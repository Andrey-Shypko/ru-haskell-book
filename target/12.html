<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
  <meta http-equiv="Content-Style-Type" content="text/css" />
  <meta name="generator" content="pandoc" />
  <title></title>
</head>
<body>
<h1 id="структурная-рекурсия">Структурная рекурсия</h1>
<p>Структурная рекурсия определяет способ построения и преобразования значений по виду типа (по составу его конструкторов). Функции, которые преобразуют значения мы будем называть <em>свёрткой</em> (fold), а функции которые строят значения – <em>развёрткой</em> (unfold). Эта рекурсия встречается очень часто, мы уже пользовались ею и не раз, но в этой главе мы остановимся на ней поподробнее.</p>
<h2 id="свёртка">Свёртка</h2>
<p> Свёртку значения можно представить как процесс, который заменяет в дереве значения все конструкторы на подходящие по типу функции.</p>
<h3 id="логические-значения">Логические значения</h3>
<p>Вспомним определение логических значений:</p>

\begin{code}
data Bool = True | False
\end{code}

<p>У нас есть два конструктора-константы. Любое значение типа \In{Bool} может состоять либо из одного конструктора \In{True}, либо из одного конструктора \In{False}. Функция свёртки в данном случае принимает две константы одинакового типа \In{a} и возвращает функцию, которая превращает значение типа \In{Bool} в значение , заменяя конструкторы на переданные значения:</p>

\begin{code}
foldBool :: a -> a -> Bool -> a
foldBool true false = \b -> case b of
    True    -> true
    False   -> false
\end{code}

<p>Мы написали эту функцию в композиционном стиле для того, чтобы подчеркнуть, что функция преобразует значение типа \In{Bool}. Определим несколько знакомых функций через функцию свёртки, начнём с отрицания:</p>

\begin{code}
not :: Bool -> Bool
not = foldNat False True
\end{code}

<p>Мы поменяли конструкторы местами, если на вход поступит \In{True}, то мы вернём \In{False} и наоборот. Теперь посмотрим на “и” и “или”:</p>

\begin{code}
(||), (&&) :: Bool -> Bool -> Bool

(||) = foldNat  (const True)  id
(&&) = foldNat  id            (const False)
\end{code}

<p>Определение функций “и” и “или” через свёртки подчёркивает, что они являются взаимно обратными. Смотрите, эти функции принимают значение типа \In{Bool} и возвращают функцию \In{Bool -> Bool}. Фактически функция свёртки для \In{Bool} является \In{if}-выражением, только в этот раз мы пишем условие в конце.</p>
<h3 id="натуральные-числа">Натуральные числа</h3>
<p>У нас был тип для натуральных чисел Пеано:</p>

\begin{code}
data Nat = Zero | Succ Nat
\end{code}

<p>Помните мы когда-то записывали определения типов в стиле классов:</p>

\begin{code}
data Nat where
    Zero :: Nat
    Succ :: Nat -> Nat
\end{code}

<p>Если мы заменим конструктор \In{Zero} на значение типа \In{a}, то конструктор \In{Succ} нам придётся заменять на функцию типа \In{a -> a}, иначе мы не пройдём проверку типов. Представим, что \In{Nat} это класс:</p>

\begin{code}
data Nat a where
    zero :: a
    succ :: a -> a
\end{code}

<p>Из этого определения следует функция свёртки:</p>

\begin{code}
foldNat :: a -> (a -> a) -> (Nat -> a)
foldNat zero succ = \n -> case n of
    Zero    -> zero
    Succ m  -> succ (foldNat zero succ m)
\end{code}

<p>Обратите внимание на рекурсивный вызов функции \In{foldNat} мы обходим всё дерево значения, заменяя каждый конструктор. Определим знакомые функции через свёртку:</p>

\begin{code}
isZero :: Nat -> Bool
isZero = foldNat True (const False)
\end{code}

<p>Посмотрим как вычисляется эта функция:</p>

\begin{code}
        isZero Zero
=>      True            -- заменили конструктор Zero

        isZero (Succ (Succ (Succ Zero)))
=>      const False (const False (const False True))
                        -- заменили и Zero и Succ
=>      False
\end{code}

<p>Что интересно за счёт ленивых вычислений на самом деле во втором выражении произойдёт лишь одна замена. Мы не обходим всё дерево, нам это и не нужно, а смотрим лишь на первый конструктор, если там \In{Succ}, то произойдёт замена на постоянную функцию, которая игнорирует свой второй аргумент и рекурсивного вызова функции свёртки не произойдёт, совсем как в исходном определении!</p>

\begin{code}
even, odd :: Nat -> Bool

even    = foldNat True  not
odd     = foldNat False not
\end{code}

<p>Эти функции определяют чётность числа, сдесь мы пользуемся тем свойством, что \In{not (not a) == a}.</p>
<p>Определим сложение и умножение:</p>

\begin{code}
add, mul :: Nat -> Nat -> Nat

add a  = foldNat a     Succ
mul a  = foldNat Zero  (add a)
\end{code}

<h3 id="maybe">Maybe</h3>
<p>Вспомним определение типа для результата частично определённых функций:</p>

\begin{code}
data Maybe a = Nothing | Just a
\end{code}

<p>Перепишем словно это класс:</p>

\begin{code}
data Maybe a b where
    Nothing :: b
    Just    :: a -> b
\end{code}

<p>Этот класс принимает два параметра, поскольку исходный тип \In{Maybe} принимает один. Теперь несложно догадаться как будет выглядеть функция свёртки, мы просто получим стандартную функцию \In{maybe}. Дадим определение экземпляра функтора и монады через свёртку:</p>

\begin{code}
instance Functor Maybe where
    fmap f = maybe Nothing (Just . f)

instance Monad Maybe where
    return      = Just
    ma >>= mf   = maybe Nothing mf ma
\end{code}

<h3 id="списки">Списки</h3>
<p>Функция свёртки для списков это функция \In{foldr}. Выведем её из определения типа:</p>

\begin{code}
data [a] = a : [a] | []
\end{code}

<p>Представим, что это класс:</p>

\begin{code}
class [a] b where
    cons    :: a -> b -> b
    nil     :: b
\end{code}

<p>Теперь получить определение для \In{foldr} совсем просто:</p>

\begin{code}
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr cons nil = \x -> case x of
    a:as    -> a `cons` foldr cons nil as
    []      -> nil    
\end{code}

<p>Мы обходим дерево значения, заменяя конструкторы методами нашего воображаемого класса. Определим несколько стандартных функций для списков через свёртку.</p>
<p>Первый элемент списка:</p>

\begin{code}
head :: [a] -> a
head = foldr const (error "empty list")
\end{code}

<p>Объединение списков:</p>

\begin{code}
(++) :: [a] -> [a] -> [a]
a ++ b = foldr (:) b a
\end{code}

<p>В этой функции мы реконструируем заново первый список но в самом конце заменяем пустой список в хвосте \In{a} на второй аргумент, так и получается объединение списков. Обратите внимание на эту особенность, скорость выполнения операции \In{(++)} зависит от длины первого списка. Поэтому между двумя выражениями</p>

\begin{code}
((a ++ b) ++ c) ++ d
a ++ (b ++ (c ++ d))
\end{code}

<p>Нет разницы в итоговом результате, но есть огромная разница по скорости вычисления! Второй гораздо быстрее. Убедитесь в этом! Реализуем объединение списка списков в один список:</p>

\begin{code}
concat :: [[a]] -> [a]
concat = foldr (++) []
\end{code}

<p>Через свёртку можно реализовать и функцию преобразования списков:</p>

\begin{code}
map :: (a -> b) -> [a] -> [b]
map f = foldr ((:) . f) []
\end{code}

<p>Если смысл выражения \In{((:) . f)} не совсем понятен, давайте распишем его типы:</p>

\begin{code}
      f           (:)
a  ------->  b  ------->  ([b] -> [b])
\end{code}

<p>Напишем функцию фильтрации:</p>

\begin{code}
filter :: (a -> Bool) -> [a] -> [a]
filter p = foldr (\a as -> foldBool (a:as) as (p a)) []
\end{code}

<p>Тут у нас целых две функции свёртки. Если значение предиката \In{p} истинно, то мы вернём все элементы списка, а если ложно отбросим первый элемент. Через \In{foldr} можно даже определить функцию с хвостовой рекурсией \In{foldl}. Но это не так просто. Всё же попробуем. Для этого вспомним определение:</p>

\begin{code}
foldl :: (a -> b -> a) -> a -> [b] -> a
foldl f s []        = s
foldl f s (a:as)    = foldl f (f s a) as
\end{code}

<p>Нам нужно привести это определение к виду \In{foldr}, нам нужно выделить два метода воображаемого класса списка \In{cons} и \In{nil}:</p>

\begin{code}
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr cons nil = \x -> case x of
    a:as    -> a `cons` foldr cons nil as
    []      -> nil    
\end{code}

<p>Перенесём два последних аргумента определения \In{foldl} в правую часть, воспользуемся лямбда-функциями и \In{case}-выражением:</p>

\begin{code}
foldl :: (a -> b -> a) -> [b] -> a -> a
foldl f = \x -> case x of    
    []      -> \s -> s
    a:as    -> \s -> foldl f as (f s a)
\end{code}

<p>Мы поменяли местами порядок следования аргументов (второго и третьего). Выделим тождественную функцию в первом уравнении \In{case}-выражения и функцию композиции во втором.</p>

\begin{code}
foldl :: (a -> b -> a) -> [b] -> a -> a
foldl f = \x -> case x of    
    []      -> id
    a:as    -> foldl f as . (flip f a)
\end{code}

<p>Теперь выделим функции \In{cons} и \In{nil}:</p>

\begin{code}
foldl :: (a -> b -> a) -> [b] -> a -> a
foldl f = \x -> case x of    
    []      -> nil 
    a:as    -> a `cons` foldl f as 
    where nil   = id
          cons  = \a b -> b . flip f a
                = \a   -> ( . flip f a)
\end{code}

<p>Теперь запишем через \In{foldr}:</p>

\begin{code}
foldl :: (a -> b -> a) -> a -> [b] -> a
foldl f s xs = foldr (\a -> ( . flip f a)) id xs s
\end{code}

<p>Кажется мы ошиблись в аргументах, ведь \In{foldr} принимает три аргумента. Дело в том, что в функции \In{foldr} мы сворачиваем списки в функции, последний аргумент предназначен как раз для результирующей функции. Отметим, что из определения можно исключить два последних аргумента с помощью функции \In{flip}.</p>
<h4 id="вычислительные-особенности-foldl-и-foldr">Вычислительные особенности foldl и foldr</h4>
<p>Если посмотреть на выражение, которое получается в результате вычисления \In{foldr} и \In{foldl} можно понять почему они так называются.</p>
<p>В левой свёртке \In{foldl} скобки группируются влево, поэтому на конце \In{l} (left):</p>

\begin{code}
foldl f s [a1, a2, a3, a4] =
    (((s `f` a1) `f` a2) `f` a3) `f` a4
\end{code}

<p>В правой свёртке \In{foldr} скобки группируются вправо, поэтому на конце \In{r} (right):</p>

\begin{code}
foldr f s [a1, a2, a3, a4]
    a1 `f` (a2 `f` (a3 `f` (a4 `f` s)))
\end{code}

<p>Кажется, что если функция \In{f} ассоциативна</p>

\begin{code}
(a `f` b) `f` c  = a `f` (b `f` c)
\end{code}


<p>то нет разницы какую свёртку применять. Разницы нет по смыслу, но может быть существенная разница в скорости вычисления. Рассмотрим функцию \In{concat}, ниже два определения:</p>

\begin{code}
concat  = foldl (++) []
concat  = foldr (++) []
\end{code}

<p>Какое выбрать? Результат и в том и в другом случае одинаковый (функция \In{++} ассоциативна). Стоит выбрать вариант с правой свёрткой. В первом варианте скобки будут группироваться влево, это чудовищно скажется на производительности. Особенно если в конце небольшие списки:</p>

\begin{code}
Prelude> let concatl  = foldl (++) []
Prelude> let concatr  = foldr (++) []
Prelude> let x = [1 .. 1000000]
Prelude> let xs = [x,x,x] ++ map return x
\end{code}

<p>Последним выражением мы создали список списков, в котором три списка по миллиону элементов, а в конце миллион списков по одному элементу. Теперь попробуйте выполнить \In{concatl} и \In{concatr} на списке \In{xs}. Вы заметите разницу по скорости печати. Также для сравнения можно установить флаг: \In{:set +s}.</p>
<p>Также интересной особенностью \In{foldr} является тот факт, что за счёт ленивых вычислений \In{foldr} не нужно знать весь список, правая свёртка может работать и на бесконечных списках, в то время как \In{foldl} не вернёт результат, пока не составит всё выражение. Например такое выражение будет вычислено:</p>

\begin{code}
Prelude> foldr (&&) undefined $ True : True : repeat False
False
\end{code}

<p>За счёт ленивых вычислений мы отбросили оставшуюся (бесконечную) часть списка. По этим примерам может показаться, что левая свёртка такая не нужна совсем, но не все операции ассоциативны. Иногда полезно собирать результат в обратном порядке, например так в \In{Prelude} определена функция \In{reverse}, которая переворачивает список:</p>

\begin{code}
reverse :: [a] -> [a]
reverse = foldl (flip (:)) []
\end{code}

<h3 id="деревья">Деревья</h3>
<p>Мы можем определить свёртку и для деревьев. Вспомним тип:</p>

\begin{code}
data Tree a = Node a [Tree a]
\end{code}

<p>Запишем в виде класса:</p>

\begin{code}
data Tree a b where
    node :: a -> [b] -> b
\end{code}

<p>В этом случае есть одна тонкость. У нас два рекурсивных типа: само дерево и внутри него – список. Для преобразования списка мы воспользуемся функцией \In{map}:</p>

\begin{code}
foldTree :: (a -> [b] -> b) -> Tree a -> b
foldTree node = \x -> case x of
    Node a as -> node a (map (foldTree node) as)
\end{code}

<p>Найдём список всех меток:</p>

\begin{code}
labels :: Tree a -> [a]
labels = foldTree $ \a bs -> a : concat bs
\end{code}

<p>Мы объединяем все метки из поддеревьев в один список и присоединяем к нему метку из текущего узла.</p>
<p>Сделаем дерево экземпляром класса \In{Functor}:</p>

\begin{code}
instance Functor Tree where
    fmap f = foldTree (Node . f)
\end{code}

<p>Очень похоже на \In{map} для списков. Вычислим глубину дерева:</p>

\begin{code}
depth :: Tree a -> Int
depth = foldTree $ \a bs -> 1 + foldr max 0 bs
\end{code}

<p>В этой функции за каждый узел мы прибавляем к результату единицу, а в списке находим максимум среди всех поддеревьев.</p>
<h2 id="развёртка">Развёртка</h2>
<p> С помощью развёртки мы постепенно извлекаем значение рекурсивного типа из значения какого-нибудь другого типа. Этот процесс очень похож на процесс вычисления по имени. Сначала у нас есть отложенное вычисление или thunk. Затем мы применяем к нему функцию редукции и у нас появляется корневой конструктор. А в аргументах конструктора снова сидят \In{thunk}’и. Мы применяем редукцию к ним. И так пока не “развернём” всё значение.</p>
<h3 id="списки-1">Списки</h3>
<p>Для разворачивания списков в \In{Data.List} есть специальная функция \In{unfoldr}. Присмотримся сначала к её типу:</p>

\begin{code}
unfoldr :: (b -> Maybe (a, b)) -> b -> [a]
\end{code}

<p>Функция развёртки принимает стартовый элемент, а возвращает значение типа пары от \In{Maybe}. Типом \In{Maybe} мы кодируем конструкторы списка:</p>

\begin{code}
data [a] b where
    (:)  :: a -> b -> b     -- Maybe (a, b)
    []   :: b               -- Nothing
\end{code}

<p>Конструктор пустого списка не нуждается в аргументах, поэтому его мы кодируем константой \In{Nothing}. Объединение принимает два аргумента голову и хвост, поэтому \In{Maybe} содержит пару из головы и следующего элемента для разворачивания. Закодируем это определение:</p>

\begin{code}
unfoldr :: (b -> Maybe (a, b)) -> b -> [a]
unfoldr f = \b -> case (f b) of
    Just (a, b') -> a : unfoldr f b'
    Nothing      -> []
\end{code}

<p>Или мы можем записать это более кратко с помощью свёртки \In{maybe}:</p>

\begin{code}
unfoldr :: (b -> Maybe (a, b)) -> b -> [a]
unfoldr f = maybe [] (\(a, b) -> a : unfoldr f b)
\end{code}

<p>Смотрите, перед нами коробочка (типа \In{b}) с подарком (типа \In{a}), мы разворачиваем, а там пара: подарок (типа \In{a}) и ещё одна коробочка. Тогда мы начинаем разворачивать следующую коробочку и так далее по цепочке, пока мы не развернём не обнаружим \In{Nothing}, это означает что подарки кончились.</p>
<p>Типичный пример развёртки это функция \In{iterate}. У нас есть стартовое значение типа \In{a} и функция получения следующего элемента \In{a -> a}</p>

\begin{code}
iterate :: (a -> a) -> a -> [a]
iterate f = unfoldr $ \s -> Just (s, f s)
\end{code}

<p>Поскольку \In{Nothing} не используется цепочка подарков никогда не оборвётся. Если только нам не будет лень их разворачивать. Ещё один характерный пример это функция \In{zip}:</p>

\begin{code}
zip :: [a] -> [b] -> [(a, b)]
zip = curry $ unfoldr $ \x -> case x of
    ([]     , _)     -> Nothing
    (_      ,[])     -> Nothing
    (a:as   , b:bs)  -> Just ((a, b), (as, bs)) 
\end{code}

<p>Если один из списков обрывается, то прекращаем разворачивать. А если оба содержат голову и хвост, то мы помещаем в голову списка пару голов, а в следующий элемент для разворачивания пару хвостов.</p>
<h3 id="потоки">Потоки</h3>
<p>Для развёртки хорошо подходят типы у которых, всего один конструктор. Тогда нам не нужно кодировать альтернативы. Например рассмотрим потоки:</p>

\begin{code}
data Stream a = a :& Stream a
\end{code}

<p>Они такие же как и списки, только без конструктора пустого списка. Функция развёртки для потоков имеет вид:</p>

\begin{code}
unfoldStream :: (b -> (a, b)) -> b -> Stream a
unfoldStream f  = \b -> case f b of
    (a, b') -> a :& unfoldStream f b'
\end{code}

<p>И нам не нужно пользоваться \In{Maybe}. Напишем функции генерации потоков:</p>

\begin{code}
iterate :: (a -> a) -> a -> Stream a
iterate f = unfoldStream $ \a -> (a, f a)

repeat :: a -> Stream a
repeat = unfoldStream $ \a -> (a, a)

zip :: Stream a -> Stream b -> Stream (a, b)
zip = curry $ unfoldStream $ \(a :& as, b :& bs) -> ((a, b), (as, bs))
\end{code}

<h3 id="натуральные-числа-1">Натуральные числа</h3>
<p>Если присмотреться к натуральным числам, то можно заметить, что они очень похожи на списки. Списки без элементов. Это отражается на функции развёртки. Для натуральных чисел мы будем возвращать не пару а просто слкедующий элемент для развёртки:</p>

\begin{code}
unfoldNat :: (a -> Maybe a) -> a -> Nat
unfoldNat f = maybe Zero (Succ . unfoldNat f)
\end{code}

<p>Напишем функцию преобразования из целых чисел в натуральные:</p>

\begin{code}
fromInt :: Int -> Nat
fromInt = unfoldNat f
    where f n
            | n == 0    = Nothing
            | n >  0    = Just (n-1)
            | otherwise = error "negative number"
\end{code}

<p>Обратите внимание на то, что в этом определении не участвуют конструкторы для \In{Nat}, хотя мы и строим значение типа \In{Nat}. Конструкторы для \In{Nat} как и в случае списков кодируются типом \In{Maybe}. Развёртка используется гораздо реже свёртки. Возможно это объясняется необходимостью кодирования типа результата некоторым промежуточным типом. Определения теряют в наглядности. Смотрим на функцию, а там \In{Maybe} и не сразу понятно <em>что</em> мы строим: натуральные числа, списки или ещё что-то.</p>
<h2 id="краткое-содержание">Краткое содержание</h2>
<p>В этой главе мы познакомились с особым видом рекурсии. Мы познакомились со структурной рекурсией. Типы определяют не только значения, но и способы их обработки. Структурная рекурсия может быть выведена из определения типа. Есть языки программирования, в которых мы определяем тип и получаем функции структурной рекурсии в подарок. Есть языки, в которых структурная рекурсия является единственным возможным способом составления рекурсивных функций.</p>
<p>Обратите внимание на то, что в этой главе мы определяли рекурсивные функции, но рекурсия встречалась лишь в определении для функции свёртки и развёртки. Все остальные функции не содержали рекурсии, более того почти все они определялись в бесточечном стиле. Структурная рекурсия это своего рода комбинатор неподвижной точки, но не общий, а специфический для данного рекурсивного типа.</p>
<p>Структурная рекурсия бывает свёрткой и развёрткой.</p>
<dl>
<dt><em>Cвёрткой</em> (fold)</dt>
<dd><p>мы получаем значение некоторого произвольного типа из данного рекурсивного типа. При этом все конструкторы заменяются на функции, которые возвращают новый тип.</p>
</dd>
<dt><em>Развёрткой</em> (unfold)</dt>
<dd><p>мы получаем из произвольного типа значение данного рекурсивного типа. Мы словно разворачиваем его из значения, этот процесс очень похож на ленивые вычисления.</p>
</dd>
</dl>
<p>Мы узнали некоторые стандартные функции структурной рекурсии: \In{cond} или \In{if}-выражения, \In{maybe}, \In{foldr}, \In{unfoldr}.</p>
<h2 id="упражнения">Упражнения</h2>
<ul>
<li><p>Определите развёртку для деревьев из модуля \In{Data.Tree}.</p></li>
<li><p>Определите с помощью свёртки следующие функции:</p>

\begin{code}
sum, prod  :: Num a => [a] -> a
or,  and   :: [Bool] -> Bool
length     :: [a] -> Int
cycle      :: [a] -> [a]

unzip      :: [(a,b)] -> ([a],[b])
unzip3     :: [(a,b,c)] -> ([a],[b],[c])
\end{code}
</li>
<li><p>Определите с помощью развёртки следующие функции:</p>

\begin{code}
infinity    :: Nat
map         :: (a -> b) -> [a] -> [b]
iterateTree :: (a -> [a]) -> a -> Tree a
zipTree     :: Tree a -> Tree b -> Tree (a, b)
\end{code}
</li>
<li><p>Поэкспериментируйте в интерпретаторе с только что определёнными функциями и теми функциями, что мы определяли в этой главе.</p></li>
<li><p>Рассмотрим ещё один стандартный тип. Он определён в \In{Prelude}. Это тип \In{Either} (дословно – один из двух). Этот тип принимает два параметра:</p>

\begin{code}
data Either a b = Left a | Right b
\end{code}

<p>Значение может быть либо значением типа \In{a}, либо значением типа \In{b}. Часто этот тип используют как \In{Maybe} с информацией об ошибке. Конструктор \In{Left} хранит сообщение об ошибке, а конструктор \In{Right} значение, если его удалось вычислить.</p>
<p>Например мы можем сделать такие определения:</p>

\begin{code}
headSafe :: [a] -> Either String a
headSafe []     = Left "Empty list" 
headSafe (x:_)  = Right x

divSafe :: Fractional a => a -> a -> Either String a
divSafe a 0 = Left "division by zero"
divSafe a b = Right (a/b)
\end{code}

<p>Для этого типа также определена функция свёртки она называется \In{either}. Не подглядывая в \In{Prelude}, определите её.</p></li>
<li><p>Список является частным случаем дерева. Список это дерево, в каждом узле которого, лишь однин дочерний узел. Деревья из модуля \In{Data.Tree} похожи на списки, но есть в них одно существенное отличие. Они всегда содержат хотя бы один элемент. Пустой список не может быть представлен в виде такого дерева. Например это различие сказывается, еслим вы захотите определить функцию-аналог \In{takeWhile} для деревьев.</p>
<p>Определите деревья, которые не страдают от этого недостатка. Определите для них функции свёртки/развёртки, а также функции, которые мы определили для стандартных деревьев. Определите функцию \In{takeWhile} (в рекурсивном виде и в виде развёртки) и сделайте их экземпляром класса \In{Monad}, похожий на экземпляр для списков.</p></li>
</ul>
</body>
</html>
