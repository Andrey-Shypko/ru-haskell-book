\setcounter{chapter}{7}
\chapter{IO}

Пока мы не написали ещё ни одной программы, которой
можно было бы пользоваться вне интерпретатора. 
Предполагается, что программа как-то взаимодействует
с пользователем (ожидает ввода с клавиатуры) и 
изменяет состояние компьютера 
(выводит сообщения на экран, записывает данные в файлы).
Но пока что мы не знаем как взаимодействовать 
с окружающим миром. 

Самое время узнать! Сначала мы посмотрим какие
проблемы связаны с реализацией взаимодействия 
с пользователем. Как эти проблемы решаются в
Haskell. Потом мы научимся решать несколько 
типичных задач, связанных с вводом/выводом.

\section{Чистота и побочные эффекты}

Когда мы определяем новые функции или константы
мы лишь даём новые имена комбинациям значений.
В этом смысле у нас ничего не изменяется.
По-другому  это называется \emph{функциональной
чистотой} (referential transparency). Это свойство 
говорит о том, что мы свободно можем заменить 
в тексте программы любой синоним на его определение
и это никак не скажется на результате.

Функция является чистой, если её выход зависит только 
от её входов. В любой момент выполнения программы 
для одних и тех же входов будет один и тот же выход.
Это свойство очень ценно. Оно облегчает понимание
поведения функции. Оно говорит о том, что функция
может зависеть от других функций только \emph{явно}.
Если мы видим, что другая функция используется
в данной функции, то она используется в этой функции. 
У нас нет таинственных глобальных переменных, в которые мы можем
записывать данные из одной функции и читать их с помощью
другой. Мы вообще не можем ничего записывать и ничего
читать. Мы не можем изменять состояния, мы можем лишь давать
новые имена или строить новые выражения из уже существующих. 

Но в этот статичный мир описаний не вписывается
взаимодействие с пользователем. Предположим, что
мы хотим написать такую программу: мы набираем на 
клавиатуре имя файла, нажимаем \In{Enter} и программа 
показывает на экране содержимое этого файла, затем мы набираем текст,
нажимаем \In{Enter} и текст дописывается в конец файла,
файл сохраняется. 
Это описание предполагает упорядоченность действий.
Мы не можем сначала сохранить текст, затем прочитать обновления.
Тогда текст останется прежним. 

Ещё один пример. Предположим у нас есть функция 
\In{getChar}, которая читает букву с клавиатуры. 
И функция \In{print}, которая выводит строку на экран
И посмотрим на такое выражение:
    
\begin{code}
let c = getChar
in  print $ c : c : []
\end{code}

О чём говорит это выражение? Возможно,
прочитай с клавиатуры букву и выведи её на 
экран дважды. Но возможен и другой
вариант, если в нашем языке все определения 
это синонимы мы можем записать это выражение так:

\begin{code}
print $ getChar : getChar : []
\end{code}

Это выражение уже говорит о том, что читать
с клавиатуры необходимо дважды! А ведь мы сделали
обычное преобразование, заменили вхождения синонима 
на его определение, но смысл изменился.
Взаимодействие с пользователем нарушает чистоту функций,
нечистые функции называются функциями с побочными эффектами.

Как быть? Можно ли внести в мир описаний порядок
выполнения, сохранив преимущества функциональной чистоты?
Долгое время этот вопрос был очень трудным
для чистых функциональных языков. Как можно пользоваться
языком, который не позволяет сделать такие базовые вещи
как ввод/вывод? 

\section{Монада IO}

\Types{IO, ввод-вывод}
Где-то мы уже встречались с такой проблемой. 
Когда мы говорили о типе \In{ST} и обновлении значений.
Там тоже были проблемы порядка вычислений, нам удалось
преодолеть их с помощью скрытой передачи фиктивного
состояния. Тогда наши обновления были \emph{чистыми},
мы могли безболезненно скрыть их от пользователя.
Теперь всё гораздо труднее. Нам всё-таки хочется
взаимодействовать с внешним миром. 
Для обозначения внешнего мира мы определим специальный
тип и назовём его \In{RealWorld}:

\begin{code}
module IO(
    IO
) where

data RealWorld = RealWorld

newtype IO a = IO (ST RealWorld a)

instance Functor        IO where ...
instance Applicative    IO where ...
instance Monad          IO where ...
\end{code}

Тип \In{IO} (от англ.~input-output или ввод-вывод)
обозначает взаимодействие с внешним миром. 
Внешний мир словно является состоянием наших вычислений.
Экземпляры классов композиции специальных функций 
такие же как и для \In{ST} (а следовательно и для \In{State}).
Но при этом, поскольку мы конкретизировали первый параметр типа
\In{ST}, мы уже не сможем воспользоваться функцией \In{runST}.


Тип \In{RealWorld} определён в модуле \In{Control.Monad.ST},
там же можно найти и функцию:

\begin{code}
stToIO :: ST RealWorld a -> IO a
\end{code}

Интересно, что класс \In{Monad} был придуман 
как раз для решения проблемы ввода-вывода. Классы типов
изначально задумывались для решения проблемы 
определения арифметических операций на разных числах
и функции сравнения на равенство для разных типов, 
мало кто тогда догадывался, что классы типов сыграют 
такую роль, станут основополагающей особенностью языка.


\Fig{Композиция для монады IO}{../pic/8/io.ps}{ioPic}{1}

Посмотрим на \RefFig{ioPic}. Это рисунок для класса \In{Kleisli}. 
Здесь под \In{>>} понимается композиция, как мы её определяли
в главе 6, а не метод класса \In{Monad}, вспомним определение:

\begin{code}
class Kleisli m where
    idK  :: a -> m a
    (>>) :: (a -> m b) -> (b -> m c) -> (a -> m c)
\end{code}

Композиция специальных функций типа \In{a -> IO b}
вносит порядок вычисления. Считается, что сначала
будет вычислена функция слева от композиции, а
затем функция справа от композиции. Это происходит
за счёт скрытой передачи фиктивного состояния.
Теперь перейдём к классу \In{Monad}. Там композиция
заменяется на применение или операция связывания:

\begin{code}
ma >>= mf
\end{code}

Для типа \In{IO} эта запись говорит о том, что сначала будет выполнено
выражение \In{ma} и результат будет подставлен в 
выражение \In{mf} и только затем будет выполнено \In{mf}.
Оператор связывания для специальных функций вида:

\begin{code}
a -> IO b
\end{code}

раскалывает наш статический мир на \Quote{до} и \Quote{после}. 
Однажды попав в сети \In{IO}, мы не можем из них 
выбраться, поскольку теперь у нас нет функции \In{runST}. 
Но это не так страшно. Тип \In{IO}
дробит наш статический мир на кадры. Но мы
спокойно можем создавать статические чистые функции
и поднимать их в мир \In{IO} лишь там где это
действительно нужно. 

Рассмотрим такой пример, программа читает с клавиатуры
начальное значение, затем загружает файл настроек. 
Потом запускается, какая-то сложная функция и 
в самом конце мы выводим результат на экран.

Схематично мы можем записать эту программу так:

\begin{code}
program = liftA2 algorithm readInit (readConfig "file") >>= print

-- функции с побочными эффектами
readInit   :: IO Int
readConfig :: String -> IO Config
print      :: Show a => a -> IO ()

-- большая и сложная, но !чистая! функция
algorithm  :: Int -> Config -> Result
\end{code}

Функция \In{readInit} читает начальное значение, 
функция \In{readConfig} читает из файла наcтройки,
функция  \In{print} выводит значение на экран,
если это значение можно преобразовать в строку. 
Функция \In{algorithm} это большая функция,
которая вычисляет какие-то данные. Фактически
наше программа это и есть функция \In{algorithm}.
В этой схеме мы добавили взаимодействие с пользователем
лишь в одном месте, вся функция \In{algorithm} 
построена по правилам мира описаний. Так мы 
внесли порядок выполнения в программу, сохранив
возможность определения чистых функций.

Если у нас будет ещё один \Quote{кадр}, ещё
одно действие, например как только функция 
\In{algorithm} закончила вычисления ей
нужны дополнительные данные от пользователя,
на основе которых мы сможем продолжить вычисления 
с помощью какой-нибудь другой функции. Тогда
наша программа примет вид:

\begin{code}
program = 
    liftA2 algorithm2 readInit 
        (liftA2 algorithm1 readInit (readConfig "file"))
    >>= print

-- функции с побочными эффектами
readInit   :: IO Int
readConfig :: String -> IO Config
print      :: Show a => a -> IO ()

-- большие и сложные, но !чистые! функции
algorithm1  :: Int -> Config -> Result1
algorithm2  :: Int -> Result1 -> Result2
\end{code}

Теперь у нас два кадра, программа выполняется в два этапа.
Каждый из них разделён участками взаимодействия с пользователем.
Но тип \In{IO} присутствует  лишь в первых шести строчках,
остальные два миллиона строк написаны в мире описаний,
исключительно чистыми функциями, которые поднимаются
в мир специальных функций с помощью функций \In{liftA2}
и стыкуются с помощью операции связывания \verb!>>=!.

Попробуем тип \In{IO} в интерпретаторе. Мы будем
пользоваться двумя стандартными функциями \In{getChar} и 
\In{print}

\begin{code}
-- читает символ с клавиатуры
getChar :: IO Char

-- выводит значение на экран
print :: IO ()
\end{code}

Функция \In{print} возвращает значение единичного
типа, завёрнутое в тип \In{IO}, поскольку нас интересует
не само значение а побочный эффект, который выполняет 
эта функция, в данном случае это вывод на экран.

Закодируем два примера из первого раздела. В первом
мы читаем один символ и печатаем его дважды:

\begin{code}
Prelude> :m Control.Applicative
Prelude Control.Applicative> let res = (\c -> c:c:[]) <$> getChar >>= print
Prelude Control.Applicative> res
q"qq"
\end{code}

Мы сначала вызываем функцию \In{getChar} удваиваем результат
функцией \verb!\!\In{c -> c:c:[]} и затем выводим на экран.

Во втором примере мы дважды запрашиваем символ с клавиатуры
а затем печатаем их:

\begin{code}
Prelude Control.Applicative> let res = liftA2 (\a b -> a:b:[]) getChar getChar >>= print
Prelude Control.Applicative> res
qw"qw"
\end{code}

\section{Как пишутся программы}

Мы уже умеем читать с клавиатуры и выводить значения
на экран. Давайте научимся писать самостоятельные
программы. Программа обозначается специальным именем: 

\begin{code}
main :: IO ()
\end{code}

Если модуль называется \In{Main} или в нём нет директивы 
\In{module ... where} и в модуле есть функция \In{main :: IO ()},
то после компиляции будет сделан исполняемый файл. Его можно запускать
независимо от \In{ghci}. Просто нажимаем
дважды мышкой или вызываем из командной строки.

Напишем программу \In{Hello world}. Единственное, что
она делает это выводит на экран приветствие:

\begin{code}
main :: IO ()
main = print "Hello World!" 
\end{code}

Теперь сохраним эти строчки в файле \In{Hello.hs}, перейдём
в директорию файла и скомпилируем файл:

\begin{code}
ghc --make Hello
\end{code}

Появились объектный и интерфейсный файлы, а также появился 
третий бинарный файл. Это либо \In{Hello} без расширения 
(в Linux) или \In{Hello.exe} (в Windows). Запустим этот файл:


\begin{code}
$ ./Hello
"Hello World!"
\end{code}

Получилось! Это наша первая программа. Теперь напишем
программу, которая принимает три символа с клавиатуры
и выводит их в обратном порядке:

\begin{code}
import Control.Applicative

f :: Char -> Char -> Char -> String
f a b c = reverse $ [a,b,c]

main :: IO ()
main = print =<< f <$> getChar <*> getChar <*> getChar
\end{code}

Сохраним в файле \In{ReverseIO.hs} и скомпилируем:

\begin{code}
ghc --make ReverseIO -o rev3
\end{code}

Дополнительным флагом \In{-o} мы попросили компилятор
чтобы он сохранил исполняемый файл под именем \In{rev3}.
Теперь запустим в командной строке:

\begin{code}
$ ./rev3
qwe
"ewq"
\end{code}

Набираем три символа и нажимаем ввод. И программа переворачивает
ответ. Обратите внимание на то, что с помощью \In{print} мы выводим 
не просто строку на экран, а строку как значение. Поэтому 
добавляются двойные кавычки. Для того чтобы выводить строку
существует функция \In{putStr}. Заменим \In{print} на \In{putStr},
перекомпилируем и посмотрим что получится:

\begin{code}
$ ghc --make ReverseIOstr -o rev3str
[1 of 1] Compiling Main             ( ReverseIOstr.hs, ReverseIOstr.o )
Linking rev3str ...
$ ./rev3str 
123
321$
\end{code}

Видно, что после вывода не произошёл перенос каретки,
терминал приглашает нас к вводу команды сразу за ответом,
если перенос нужен, можно воспользоваться функцией \In{putStrLn}.
Обратите внимание на то, что кроме бинарного файла 
появились ещё два файла с расширениями \In{.hi} и \In{.o}. Первый
файл называется интерфейсным он описывает какие в модуле 
определения, а второй файл называется объектным. Он содержит
скомпилированный код модуля. 

\section{Типичные задачи IO}

\subsection{Вывод на экран}

Нам уже встретилось несколько функций вывода на экран.
Это функции: \In{print} (вывод значения из экземпляра класса \In{Show}),
\In{putStr} (вывод строки) и \In{putStrLn} (вывод строки с переносом).
Каждый раз когда мы набираем какое-нибудь выражение в строке
интерпретатора и нажимаем \In{Enter}, интерпретатор применяет
к выражению функцию \In{print} и мы видим его на экране.

Из простейших функций вывода на экран осталось не рассмотренной
лишь функция \In{putChar}, но я думаю вы без труда догадаетесь
по типу и имени чем она занимается:

\begin{code}
putChar :: Char -> IO ()
\end{code}

Функции вывода на экран также можно вызывать в интерпретаторе:


\begin{code}
Prelude> putStr "Hello" >> putChar ' ' >> putStrLn "World!" 
Hello World!
\end{code}

Обратите внимание на применение постоянной функции для монад
\verb!>>!. В этом выражении нас интересует не результат, 
а те побочные эффекты, которые выполняются при композиции
специальных функций. Также мы пользовались функцией \verb!>>!
в сочетании с монадой \In{Writer} для накопления результата.

\subsection{Ввод пользователя}

Мы уже умеем принимать от пользователя буквы. Это
делается функцией \In{getChar}. Функцией \In{getLine}
мы можем прочитать целую строчку. Строка читается
до тех пор пока мы не нажмём \In{Enter}.

\begin{code}
Prelude> fmap reverse $ getLine
Hello-hello!
"!olleh-olleH"
\end{code}

Есть ещё одна функция для чтения строк, она называется
\In{getContents}. Основное отличие от \In{getLine} 
заключается в том, что содержание не читается сразу,
а откладывается на потом, когда содержание действительно
понадобится. Это ленивый ввод. Для задачи чтения символов
с терминала эта функция может показаться странной.
Но часто в символы вводятся не вручную, а передаются из 
другого файла. Например если мы направим на ввод данные
из-какого-нибудь большого-большого файла, файл не будет
читаться сразу, и память не будет заполнена не нужным 
пока содержанием. Вместо этого программа отложит считывание
на потом и будет заниматься им лишь тогда, когда оно понадобится
в вычислениях. Это может существенно снизить расход памяти.
Мы читаем файл в 2Гб моментально (мы делаем вид, что читаем его).
А на самом деле сохраняем себе задачу на будущее: читать ввод,
когда придёт пора.

\subsection{Чтение и запись файлов}

Для чтения и записи файлов есть три простые функции:

\begin{code}
type FilePath = String

-- чтение файла
readFile    :: FilePath -> IO String

-- запись строки в файл
writeFile   :: FilePath -> String -> IO ()

-- добавление строки в конеци файла
appendFile  :: FilePath -> String -> IO ()
\end{code}

Напишем программу, которая сначала запрашивает путь к файлу.
Затем показывает его содержание. Затем запрашивает ввод строки
из терминала. А после этого добавляет текст в конец файла.

\begin{code}
main = msg1 >> getLine >>= read >>= append
    where read   file = readFile file >>= putStrLn >> return file
          append file = msg2 >> getLine >>= appendFile file
          msg1        = putStr "input file: "
          msg2        = putStr "input text: "
\end{code}

В самом левом вызове \In{getLine} мы читаем имя файла, затем
оно используется в локальной функции \In{read}. Там мы читаем
содержание файла (\In{readLine}), выводим его на экран (\In{putStrLn}),
и в самом конце мы возвращаем из функции имя файла. Оно нам понадобится
в следующей части программы, в которой мы будем читать новые записи
и добавлять их в файл. Новая запись читается функцией \In{getLine}
в локальной функции \In{append}.

Сохраним в модуле \In{File.hs} и посмотрим, что у нас получилось.
Перед этим создадим в текущей директории тестовый
пустой файл под именем \In{test}. В него мы будем добавлять новые
записи. 

\begin{code}
*Prelude> :l File
[1 of 1] Compiling File             ( File.hs, interpreted )
Ok, modules loaded: File.
*File> main
input file: test

input text: Hello!
*File> main
input file: test
Hello!
input text: Hi)
*File> main
input file: test
Hello!Hi)
\end{code}

В самом начале наш файл пуст, поэтому сначала мы видим
пустую строчку вместо содержания, но потом мы начинаем добавлять
в него новые записи.

\subsection{Аргументы программы}

Пока программы, которые мы создавали просили пользователя 
ввести данные вручную при выполнении программы,
они работали в интерактивном режиме, но чаще всего программы
принимают какие-нибудь начальные данные, установки или флаги. 
Читать начальные данные можно с помощью функций из модуля 
\In{System.Environment}. 

Узнать, что передаётся в программу можно функцией 
\In{getArgs :: IO [String]}. Она возвращает список строк. Это те строки, что
мы написали за именем программы через пробел при вызове в терминале.
Напишем простую программу, которая распечатывает свои аргументы
по порядку, в виде пронумерованного списка.

\begin{code}
module Main where

import System.Environment

main = getArgs >>= mapM_ putStrLn . zipWith f [1 .. ]
    where f n a = show n ++ ": " ++ a
\end{code}

В локальной функции \In{f} мы присоединяем к строке номер
через двоеточие. Функцией \In{mapM_} мы пробегаем по списку
строк, отображая их с помощью функции \In{putStrLn}. 
Обратите внимание на краткость программы, с помощью функции композиции
мы легко составили функцию, которая приписывает к аргументам
числа, а затем выводит их на экран.

Скомпилируем программу в интерпретаторе и вызовем её.

\begin{code}
*Main> :! ghc --make Args
[1 of 1] Compiling Main             ( Args.hs, Args.o )
Linking Args ...
*Main> :! ./Args hey hey hey 23 54 "qwe qwe qwe" fin
1: hey
2: hey
3: hey
4: 23
5: 54
6: qwe qwe qwe
7: fin
\end{code}

Если мы хотим, чтобы аргумент-строка содержал пробелы мы 
заключаем его в двойные кавычки. 


С помощью функции \In{getProgName} можно узнать имя программы.
Создадим программу, которая здоровается при вызове. И отвечает
в зависимости от настроения программы. Настроение
задаётся аргументом программы. 

\begin{code}
module Main where

import Control.Applicative
import System.Environment

main = putStrLn =<< reply <$> getProgName <*> getArgs

reply :: String -> [String] -> String
reply name (x:_) = hi name ++ case x of
    "happy"     -> "What a lovely day. What's up?"
    "sad"       -> "Ooohh. Have you got some news for me?"
    "neutral"   -> "How are you?"  
reply name _     = reply name ["neutral"]


hi :: String -> String
hi name = "Hi! My name is " ++ name ++ ".\n"
\end{code}

В функции \In{reply} мы составляем реплику программы.
Она зависит от имени программы и поступающих на
вход аргументов. Посмотрим, что у нас получилось:

\begin{code}
*Main> :! ghc --make HowAreYou.hs -o ninja 
[1 of 1] Compiling Main             ( HowAreYou.hs, HowAreYou.o )
Linking ninja ...
*Main> :! ./ninja happy
Hi! My name is ninja.
What a lovely day. What's up?
*Main> :! ./ninja sad
Hi! My name is ninja.
Ooohh. Have you got some news for me?
\end{code}

\subsection{Вызов других программ}

Мы можем вызвать любую программу из нашей программы. 
Это делается с помощью функции \In{system}, которая 
живёт в модуле \In{System}. 

\begin{code}
system :: String -> IO ExitCode
\end{code}

Она принимает строку и запускает её в терминале. Так же
как мы делали это с помощью приставки \In{:!} в интерпретаторе.
Значение типа \In{ExitCode} говорит о результате выполнения 
строки. Он может быть успешным, тогда функция вернёт 
\In{ExitSuccess} и закончиться ошибкой, тогда мы сможем
узнать код ошибки по значению \In{ExitFailure Int}.

\subsection{Случайные значения}

Функции для создания случайных значений определены в
модуле \In{System.Random}%
\footnote{Модуль \In{System.Random} входит в библиотеку \In{random}.%
Если в вашей поставке \In{ghc} его не оказалось, вы можете
установить его вручную через интернет, набрав в командной строке \In{cabal install random}.}. 
Сначала давайте разберёмся как генерируются
случайные числа.
Стандартные случайные числа очень похожи на те, что были
у нас, когда мы рассматривали примеры специальных функций. 
У нас есть генератор случайных чисел типа \In{g} и с помощью функции
\In{next} мы можем получить обновлённый генератор и
случайное целое число:

\begin{code}
next :: g -> (Int, g)
\end{code}

Не правда ли этот тип очень похож на тип результата
функций с состоянием. В качестве состояния теперь выступает
генератор случайных чисел \In{g}. Это поведение 
описывается классом \In{RandomGen}:

\begin{code}
class RandomGen g where
    next     :: g -> (Int, g)
    split    :: g -> (g, g)
    geтRange :: g -> (Int, Int)
\end{code}

Функция \In{next} обновляет генератор и возвращает случайное
значение типа \In{Int}. Функция \In{split} раскалывает один
генератор на два. Функция \In{genRange} возвращает диапазон
значений генерируемых случайных чисел. Первое значение в паре
результата \In{genRange} должно быть всегда меньше второго. 
Для этого класса определён один экземпляр, это тип \In{StdGen}.
Мы можем создать первый генератор по целому числу с помощью
функции \In{mkStdGen}:

\begin{code}
mkStdGen :: Int -> StdGen
\end{code}

Давайте посмотрим как это происходит в интерпретаторе:

\begin{code}
Prelude> :m System.Random
Prelude System.Random> let g0 = mkStdGen 0
Prelude System.Random> let (n0, g1) = next g0
Prelude System.Random> let (n1, g2) = next g1
Prelude System.Random> n0
2147482884
Prelude System.Random> n1
2092764894
\end{code}

Мы создали первый генератор, а затем начали получать новые.
Для того, чтобы получать новые случайные числа, нам придётся
таскать везде за собой генератор случайных чисел. Мы можем
обернуть его в функцию с состоянием и пользоваться 
методами классов \In{Functor}, \In{Applicative} и \In{Monad}.
Обновление генератора будет происходить за ширмой, во
время применения функций. Но у нас есть и другой путь.

Вместо монады \In{State} мы можем воспользоваться 
монадой \In{IO}. Если нам лень определять генератор случайных чисел, 
мы можем попросить компьютер определить его за нас. В этом случае мы 
взаимодействуем с компьютером, мы запрашиваем 
глобальное для системы случайное значение, поэтому
возвращаемое значение будет завёрнуто в тип \In{IO}. 
Для этого определены функции:

\begin{code}
getStdGen :: IO StdGen
newStdGen :: IO StdGen
\end{code}

Функция \In{getStdGen} запрашивает глобальный для системы 
генератор случайных чисел. Функция \In{newStdGen} не
только запрашивает генератор, но также и обновляет его.
Мы пользуемся этими функциями так же как и \In{mkStdGen},
только теперь мы спрашиваем первый аргумент у компьютера, а не передаём
его вручную. Также есть ещё одна полезная функция:

\begin{code}
getStdRandom    :: (StdGen -> (a, StdGen)) -> IO a
\end{code}

Посмотрим, что получится, если передать в неё функцию \In{next}:

\begin{code}
Prelude System.Random> getStdRandom next 
1386438055
Prelude System.Random> getStdRandom next 
961860614
\end{code}

И не надо обновлять никаких генераторов. Но вместо одного
неудобства мы получили другое. Теперь значение завёрнуто в оболочку \In{IO}.

Генератор \In{StdGen} делает случайные числа из
диапазона всех целых чисел. Что если мы хотим получить 
только числа из некоторого интервала? И как получить 
случайные значения других типов?
Для этого существует класс \In{Random}. Он является
удобной надстройкой над классом \In{RandomGen}.
Посмотрим на его основные методы:

\begin{code}
class Random a where
    randomR :: RandomGen g => (a, a) -> g -> (a, g)
    random  :: RandomGen g => g -> (a, g)
\end{code}

Метод \In{randomR} принимает диапазон значений, генератор
случайных чисел и возвращает случайное число из указанного
диапазона и обновлённый генератор. Метод \In{random} 
является синонимом метода \In{next} из класса \In{RandomGen},
только теперь мы можем получать не только целые числа.

Есть и дополнительные методы. Есть методы, которые позволяют
генерировать список всех возможных случайных значений
для данного генератора:

\begin{code}
    randomRs :: RandomGen g => (a, a) -> g -> [a]
    randoms  :: RandomGen g => g -> [a]
\end{code}

За счёт лени мы будем получать новые значения по 
мере необходимости. 

\begin{code}
    randomRIO   :: (a, a) -> IO a
    randomIO    :: IO a
\end{code}

Эти функции выполняют тоже, что и основные функции
класса, но им не нужен генератор случайных чисел, они
создают его с помощью функции \In{getStdRandom}.
Экземпляры \In{Random} определены для 
\In{Bool}, \In{Char}, \In{Double}, \In{Float}, \In{Int}	и
\In{Integer}. Например так мы можем подбросить кости
десять раз:

\begin{code}
Prelude System.Random> fmap (take 10 . randomRs (1, 6)) getStdGen
[5,6,5,5,6,4,6,4,4,4]
Prelude System.Random> fmap (take 10 . randomRs (1, 6)) getStdGen
[5,6,5,5,6,4,6,4,4,4]
\end{code}

Обратите внимание на то, что функция \In{getStdGen} не
обновляет генератор случайных чисел. Мы запрашиваем 
глобальное состояние. Поэтому, дважды подбросив кубик,
мы получили одни и те же результаты. Генератор 
будет обновляться, если воспользоваться функцией
\In{newStdGen}:

\begin{code}
Prelude System.Random> fmap (take 10 . randomRs (1, 6)) newStdGen
[1,1,5,6,5,2,5,5,5,3]
Prelude System.Random> fmap (take 10 . randomRs (1, 6)) newStdGen
[5,4,6,5,5,5,1,5,5,2]
\end{code}

Создадим случайные слова из пяти букв:

\begin{code}
Prelude System.Random> fmap (take 5 . randomRs ('a', 'z')) newStdGen
"maclg"
Prelude System.Random> fmap (take 5 . randomRs ('a', 'z')) newStdGen
"nfjoa"
\end{code}


\subsubsection{Цитатник}

Напишем небольшую программу, которая будет выводить
на экран в случайном порядке цитаты. Цитаты хранятся
в виде списка пар \In{(автор, высказывание)}. Сначала
мы генерируем случайное число в диапазоне длины списка,
затем выбираем цитату под этим номером и выводим её
на экран. 

\begin{code}
module Main where

import Control.Applicative
import System.Random

main = 
    format . (quotes !! ) <$> randomRIO (0, length quotes - 1)
    >>= putStrLn

format (a, b) = b  ++ space ++ a ++ space
    where space = "\n\n"

quotes = [
    ("Бьёрн Страуструп", 
     "Есть лишь два вида языков программирования: те, \
        \ на которые вечно жалуются, и те, которые никогда \
        \ не используются."),
    ("Мохатма Ганди", "Ты должен быть теми изменениями, которые\
        \ ты хочешь видеть вокруг."),
    ("Сократ", "Я знаю лишь то, что ничего не знаю."),
    ("Китайская народная мудрость", "Сохранив спокойствие в минуту\
     \ гнева, вы можете избежать сотни дней сожалений"),
    ("Жан Батист Мольер", "Медленно растущие деревья приносят лучшие плоды"),
    ("Антуан де Сент-Экзюпери", "Жить это значит медленно рождаться"),
    ("Альберт Эйнштейн", "Фантазия важнее знания."),
    ("Тони Хоар", "Внутри любой большой программы всегда есть\
     \ маленькая, что рвётся на свободу"),
    ("Пифагор", "Не гоняйся за счастьем, оно всегда находится в тебе самом"),
    ("Лао Цзы", "Путешествие в тысячу ли начинается с одного шага")]
\end{code}

Функция \In{format} приводит цитату к виду приятному для чтения.
Попробуем программу в интерпретаторе:

\begin{code}
Prelude> :! ghc --make Quote -o hi
[1 of 1] Compiling Main             ( Quote.hs, Quote.o )
Linking hi ...
Prelude> :! ./hi
Путешествие в тысячу ли начинается с одного шага

Лао Цзы


Prelude> :! ./hi
Не гоняйся за счастьем, оно всегда находится в тебе самом

Пифагор
\end{code}


\subsection{Исключения}

Мы уже знаем несколько типов, с помощью которых функции
могут сказать, что что-то случилось не так. Это типы 
\In{Maybe} и \In{Either}. Если функции не удалось вычислить
значение она возвращает специальное значение \In{Nothing}
или \In{Left reason}, по которому следующая функция
может опознать ошибку и предпринять какие-нибудь действия.
Так обрабатываются ошибки в чистых функциях.
В этом разделе мы узнаем о том, как обрабатываются 
ошибки, которые происходят при взаимодействии с
внешним миром, ошибки, которые происходят внутри
типа \In{IO}. 

Ошибки функций с побочными эффектами обрабатываются
с помощью специальной функции \In{catch}, она определена 
в \In{Prelude}:

\begin{code}
catch :: IO a -> (IOError -> IO a) -> IO a
\end{code}

Эта функция принимает значение, которое содержит побочные
эффекты и функцию, которая обрабатывает исключительные 
ситуации. К примеру если мы попытаемся прочитать данные
из файла, к которому у нас нет доступа, произойдёт ошибка.
Мы можем не дать программе упасть и обработать ошибку
с помощью функции \In{catch}.

Например программа, в которой мы дописывали данные в
файл, упадёт, если мы передадим не существующий файл.
Но мы можем исправить это поведение с помощью функции
\In{catch}. Мы можем перезапускать программу, если произошла
ошибка:

\begin{code}
module FileSafe where

import Control.Applicative
import Control.Monad

main = try `catch` const main

try = msg1 >> getLine >>= read >>= append
    where read   file = readFile file >>= putStrLn >> return file
          append file = msg2 >> getLine >>= appendFile file
          msg1        = putStr "input file: "
          msg2        = putStr "input text: "
\end{code}

Часто функции двух аргументов называют так, чтобы
при инфиксной форме записи получалась фраза из английского языка.
Так если мы запишем \In{catch} в инфиксной форме получится
очень наглядное выражение. 
Функция обработки ошибок реагирует на любую ошибку
перезапуском программы. Попробуем взломать программу:

\begin{code}
*FileSafe> main
input file: fsldfksld
input file: sd;fls;dfl;vll; d;fld;f
input file: dflks;ldkf ldkfldkfld
input file: lsdkfksdlf ksdkflsdfkls;dfk
input file: bfk
input file: test
Hello!Hi)
input text: HowHow
\end{code}

Функция будет запрашивать файл до тех пор, пока мы 
не введём корректное значение. Мы можем добавить 
сообщение об ошибке, немного изменив функцию обработки:

\begin{code}
main = try `catch` const (msg >> main)
    where msg = putStrLn "Wrong filename, try again."
\end{code}

А что делать если нам хочется различать ошибки по типу
и предпринимать различные действия в зависимости от
типа ошибки? 
Ошибки распознаются с помощью специальных предикатов, 
которые определены в модуле \In{System.IO.Error}.
Рассмотрим некоторые из них.

Например с помощью с помощью предиката \In{isDoesNotExistErrorType}
мы можем опознать ошибки, которые случились из-за того, что
один из аргументов функции не существует. С помощью предиката
\In{isPermissionErrorType} мы можем узнать, что ошибка произошла
из-за того, что мы пытались получить доступ к данным, на
которые у нас нет прав. Мы можем, немного изменив 
функцию-обработчик исключений, выводить более информативные
сообщения об ошибках перед перезапуском:


\begin{code}
main = try `catch` handler

handler :: IOError -> IO ()
handler = ( >> main) . putStrLn . msg2 . msg1

msg1 e 
    | isDoesNotExistErrorType e = "File does not exist. "
    | isPermissionErrorType e   = "Access denied. "
    | otherwise                 = ""

msg2 = (++ "Try again.")
\end{code}

В модуле \In{System.IO.Error} вы можете найти ещё много
разных предикатов. 

\subsection{Потоки текстовых данных}

Обмен данными, чтение и запись происходят с помощью
потоков. Каждый поток имеет \emph{дескриптор} (handle),
через него мы можем общаться с потоком, например считывать 
данные или записывать. Функции для работы с потоками 
данных определены в модуле \In{System.IO}. 

В любой момент в системе открыты три стандартных потока:
\begin{itemize}
\item \In{stdin}   -- стандартный ввод
\item \In{stdout}  -- стандартный вывод
\item \In{stderr}  -- поток ошибок и отладочных сообщений
\end{itemize}

Например когда мы выводим строку на экран, на самом деле
мы записываем строку в поток \In{stdout}. А когда мы читаем
символ с клавиатуры, мы считываем его из потока \In{stdin}.

Файлы также являются потоками. При открытии файлу
присваивается дескриптор через который, мы можем обмениваться 
данными. Файл может быть открыт для чтения, записи, 
дополнения (записи в конец файла) или чтения и записи. 
Файл открывается функцией:

\begin{code}
openFile :: FilePath -> IOMode -> IO Handle
\end{code}

Функция принимает путь к файлу и режим работы с файлом
и возвращает дескриптор. Режим может принимать одно из значений:

\begin{itemize}
\item \In{ReadMode}         -- чтение
\item \In{WriteMode}        -- запись
\item \In{AppendMode}       -- добавление (запись в конец файла)
\item \In{ReadWriteMode}    -- чтение и запись
\end{itemize}

Открыв дескриптор, мы можем начать обмениваться данными.
Для этого определены функции аналогичные тем, что мы уже
рассмотрели. Функции для записи данных:

\begin{code}
-- запись символа
hPutChar :: Handle -> Char -> IO ()

-- запись строки
hPutStr :: Handle -> String -> IO ()

-- запись строки с переносом каретки
hPutStrLn :: Handle -> String -> IO ()

-- запись значения
hPrint :: Show a => Handle -> a -> IO ()
\end{code}

Все функции принимают первым аргументом дескриптор потока. 
Дескриптор должен позволять записывать данные. Например
для дескриптора, открытого в режиме \In{ReadMode}, выполнение
этих функций приведёт к ошибке.

Из потоков также можно читать данные. Эти функции похожи 
на те, что мы уже рассмотрели:

\begin{code}
-- чтение одного символа
hGetChar :: Handle -> IO Char

-- чтение строки
hGetLine :: Handle -> IO String

-- ленивое чтение строки
hGetContents :: Handle -> IO String
\end{code}

Как только, мы закончим работу с файлом, его необходимо
закрыть. Нам нужно освободить дескриптор. Сделать это можно
функцией \In{hClose}:

\begin{code}
hClose :: Handle -> IO ()
\end{code}

Стандартные функции ввода/вывода, которые мы рассмотрели ранее
определены через функции работы с дескрипторами. 
Например так мы выводим строку на экран:

\begin{code}
putStr          :: String -> IO ()
putStr s        =  hPutStr stdout s
\end{code}

А так читаем строку с клавиатуры:

\begin{code}
getLine         :: IO String
getLine         =  hGetLine stdin
\end{code}

В этих функциях используются дескрипторы стандартных потоков
данных \In{stdin} и \In{stdout}. Отметим функцию \In{withFile}:

\begin{code}
withFile :: FilePath -> IOMode -> (Handle -> IO r) -> IO r
\end{code}

Она открывает файл в заданном режиме выполняет функцию 
на его дескрипторе и и закрывает файл. Например через 
эту функцию определены функции \In{readFile} и \In{appendFile}:

\begin{code}
appendFile      :: FilePath -> String -> IO ()
appendFile f txt = withFile f AppendMode (\hdl -> hPutStr hdl txt)

writeFile :: FilePath -> String -> IO ()
writeFile f txt = withFile f WriteMode (\hdl -> hPutStr hdl txt)
\end{code}

\section{Форточка в мир побочных эффектов}

В самом начале главы я сказал о том, что из мира \In{IO}  
нет выхода. Нет функции с типом \In{IO a -> a}. На самом
деле выход есть. Функция с таким типом живёт в модуле 
\In{System.IO.Unsafe}:

\begin{code}
unsafePerformIO :: IO a -> a
\end{code}

Длинное имя функции намекает на то, что её необходимо
использовать с \emph{крайней} осторожностью. Поскольку
последствия могут быть непредсказуемыми. 

Эта функция используется при чтении конфигурационных файлов.
Если есть уверенность в том, что файл будет только читаться
и во время выполнения программы файл не может быть изменён 
другой программой, то мы можем считать, что его значение окажется
неизменным на протяжении работы программы. Это говорит о том, что 
нам не важно когда читать данные. Поэтому здесь мы вроде бы
ничем не рискуем. \Quote{Вроде бы} потому что ответственность
за постоянство файла лежит на наших плечах. 

Эта функция часто используется при вызове функций С через
Haskell. В Haskell есть возможность вызывать функции,
написанные на C. Но по умолчанию такие функции заворачиваются
в тип \In{IO}. Если функция является
чистой в С, то она будет чистой и при вызове через Haskell. 
Мы можем поручиться за её чистоту и вычислитель нам поверит.
Но если мы его обманули, мы пожнём плоды своего обмана.

\subsection{Отладка программ}

Раз уж речь зашла о \Quote{грязных} возможностях 
языка стоит упомянуть функцию \In{trace} из модуля
\In{Debug.Trace}. Посмотрим на её тип:

\begin{code}
trace :: String -> a -> a
\end{code}

Это служебная функция эхо-печати. Когда дело доходит до вычисления 
функции \In{trace} на экран выводится строка, которая была передана
в неё первым аргументом, после чего функция возвращает второй аргумент.
Это функция \In{id} с побочным эффектом вывода сообщения на экран.
Ею можно пользоваться для отладки. Например так можно вернуть
значение и распечатать его:

\begin{code}
echo :: Show a => a -> a
echo a = trace (show a) a
\end{code}



\section{Композиция монад}

Эта глава завершает наше путешествие в мире типов-монад.
Мы начали наше знакомство с монадами с композиции, 
мы определили класс \In{Monad} через класс \In{Kleisli},
который упрощал составление специальных функций
вида \In{a -> m b}. Тогда мы познакомились с самыми простыми
типами монадами (списки и частично определённые функции),
потом мы перешли к типам посложнее, мы научились проводить
вычисления с состоянием. В этой главе мы рассмотрели
самый важный тип монаду \In{IO}. Мне бы хотелось замкнуть
этот рассказ на теме композиции. Мы поговорим о композиции
нескольких монад.  
Если вы посмотрите в исходный код библиотеки \In{transformers},
то увидите совсем другое определение для \In{State}:

\begin{code}
type State s = StateT s Identity

newtype StateT s m a = StateT { runStateT :: s -> m (a,s) }
newtype Identity a = Identity { runIdentity :: a }
\end{code}

Но так ли оно далеко от нашего? Давайте разберёмся.
\In{Identity} это тривиальный тип обёртка. Мы просто
заворачиваем значение в конструктор и ничего с ним не делаем.
Вы наверняка сможете догадаться как определить экземпляры
всех рассмотренных в этой главе классов для этого типа. 
Тип \In{StateT} больше похож на наше определение для \In{State},
единственное отличие -- это дополнительный параметр \In{m} в который
завёрнут результат функции обновления состояния.
Если мы сотрём In{m}, то получим наше определение. 
Это и сказано в определении для \In{State}

\begin{code}
type State s = StateT s Identity
\end{code}

Мы передаём дополнительным параметром в \In{StateT} тип \In{Identity},
который как раз ничего и не делает с типом. Так мы получим
наше исходное определение, но зачем такие премудрости?
Такой тип принято называть \emph{монадным трансформером} (monad transformer). 
Он определяет композицию из нескольких монад в данном случае 
одной из монад является \In{State}. Посмотрим на экземпляр класса
\In{Monad} для \In{StateT}

\begin{code}
instance (Monad m) => Monad (StateT s m) where
    return a = StateT $ \s -> return (s, a)
    
    a >>= f = StateT $ \s0 ->
        runStateT a s0 >>= \(b, s1) -> runStateT (f b) s1            
\end{code}

В этом определении мы пропускаем состояние через сито
методов класса \In{Monad} для типа \In{m}. В остальном это определение
ничем не отличается от нашего. Также определены и \In{ReaderT},
\In{WriterT}, \In{ListT} и \In{MaybeT}. Ключевым классом для всех этих
типов является класс `MonadTrans`:

\begin{code}
class MonadTrans t where
    lift :: Monad m => m a -> t m a 
\end{code}

Этот тип позволяет нам заворачивать специальные значения
типа \In{m} в значения типа \In{t}. Посмотрим на определение для 
\In{StateT}: 

\begin{code}
instance MonadTrans (StateT s) where
    lift m = StateT $ \s -> liftM (,s) m
\end{code}

Напомню, что функция \In{liftM} это тоже самое , что и функция \In{fmap},
только она определена через методы класса \In{Monad}.
Мы создали функцию обновлнения состояния, которая ничего
не делает с состоянием, а лишь прицепляет его к значению.

Приведём простой пример применения трансформеров. 
Вернёмся к примеру \In{FSM} из предыдущей главы. 
Предположим, что наш конечный автомат не только реагирует
на действия, но и ведёт журнал, в который записываются 
все поступающие на вход события. За переход состояний будет
по прежнему отвечать тип \In{State} только теперь он станет
трансформером, для того чтобы включить воможность
журналирования. За ведение журнала будет отвечать
тип \In{Writer}. Ведь мы просто накапливаем записи.

Интересно, что для добавления новой возможности нам
нужно изменить лишь определение типа \In{FSM} и функцию
\In{fsm}, теперь они примут вид:

\begin{code}
module FSMt where

import Control.Monad.Trans.Class
import Control.Monad.Trans.State
import Control.Monad.Trans.Writer

import Data.Monoid

type FSM s = StateT s (Writer [String]) s

fsm :: Show ev => (ev -> s -> s) -> (ev -> FSM s)
fsm transition e = log e >> run e
    where run e = StateT $ \s -> return (s, transition e s)  
          log e = lift $ tell [show e] 
\end{code}

Все остальные функции останутся прежними. 
Сначала мы подключили все необходимые модули из
библиотеки \In{transformers}. 
В подфункции \In{log} мы сохраняем сообщение в журнал,
а в подфункции \In{run} мы выполняем функцию перехода.
Посмотрим, что у нас получилось:

\begin{code}
*FSMt> let res = mapM speaker session
*FSMt> runWriter $ runStateT res (Sleep, Level 2)
(([(Sleep,Level 2),(Work,Level 2),(Work,Level 3),(Work,Level 2),
 (Sleep,Level 2)],(Sleep,Level 3)),
 ["Button","Louder","Quieter","Button","Louder"])
*FSMt> session
[Button,Louder,Quieter,Button,Louder]
\end{code}

Мы видим, что цепочка событий была успешно записана
в журнал. 

Для трансформеров с типом \In{IO} определён специальный класс:

\begin{code}
class Monad m => MonadIO m where
    liftIO :: IO a -> m a
\end{code}

Этот класс живёт в модуле \In{Control.Monad.IO.Class}. 
С его помощью мы можем выполнять \In{IO}-действия 
ввнутри другой монады. Эта возможность бывает 
очень полезной. Вам она обязательно понадобится, если вы 
начнёте писать веб-сайты на Haskell (например в \In{happstack})
или будете пользоваться библиотеками, которые
надстроены над C (например физический движок \In{Hipmunk}). 


\section{Краткое содержание}

Наконец-то мы научились писать программы! Программы, которые
можно исполнять за пределами интерпретатора. Для этого нам
пришлось познакомиться с типом \In{IO}. Экземпляр класса
\In{Monad} для этого типа интерпретируется специальным образом. 
Он вносит упорядоченность в выполнение программы. В нашем статическом
мире описаний появляются такие понятия как \Quote{сначала},
\Quote{затем}, \Quote{до} и \Quote{после}. Но они не смогут
нанести много вреда. 

Вычисление операций с побочными эффектами разбивает
программу на кадры. Но это не мешает нам писать основные
функции в чистом виде, подставляя их по мере необходимости
в изменчивый мир побочных эффектов 
с помощью методов из классов \In{Functor}, \In{Applicative},
\In{Monad}.

Мы узнали как в Haskell обстоят дела с такими типичными
задачами мира побочных эффектов как ввод/вывод, чтение/запись файлов,
генерация случайных значений, выполнение внешних программ,
инициализация программ с помощью флагов. Также мы узнали
о том, как обрабатываются специфические для типа \In{IO} 
исключения.


\section{Упражнения}

Старайтесь свести присутствие функций с побочными 
эффектами к минимуму. Идеальный случай, когда тип \In{IO}
встречается только в функции \In{main}. Часто программы
устроены более хитрым образом и функции с побочными 
эффектами пытаются расползтись по всему коду. Но даже
в этом случае программу можно разделить на две части:
в одной живут подлинные источники побочных эффектов, 
такие как чтение файлов, генерация случайных значений,
а в другой -- чистые функции. Старайтесь устроить программу
так, чтобы она была максимально чистой. Чистые функции
гораздо проще комбинировать, понимать, изменять.  

\begin{itemize}

\item Это упражнение даёт вам возможность почувствовать
    преимущества чистого кода. Вспомните функцию поиска
    корней методом неподвижной точки (этот пример встречался
    в главе о ленивых вычислениях). Напишите на основе
    этого примера программу, которая будет распечатывать
    решение и последовательность приближений. Последовательность
    приближений состоит из текущего значения корня и 
    расстоянии между корнями. 

    Напишите два варианта программы, в одном вы измените алгоритм
    так, чтобы печать происходила одновременно с вычислениями 
    (не пользуясь функцией из модуля \In{Debug.Trace}). 
    А в другом варианте алгоритм останется прежним. Но
    теперь вместо решения найдите список первых приближений до
    решения. А затем передайте его в отдельную функцию 
    печати результатов.

    В первом варианте алгоритм смешан с печатью. А во втором
    программа распадается на две части, часть вычислений и
    часть вывода результатов на экран. Сравните два подхода.
    
\item Напишите программу для угадывания чисел. Компьютер
    загадал число в заданном диапазоне и просит вас угадать его.
    Если вы ошибаетесь он подсказывает: \Quote{холодно-горячо}
    или \Quote{больше-меньше}. Программа принимает два аргумента,
    которые определяют диапазон возможных значений для неизвестного
    числа.

\item С помощью стандартных функций для генерации случайных
    чисел напишите программу, которая проводит состязание
    по игре в кости. Программа принимает аргументом суммарное число
    очков необходимых для победы. Двое игроков бросают по очереди 
    кости побеждает тот, кто первым наберёт заданную сумму.

    Сделайте так чтобы результаты выводились постепенно. С каждым
    нажатием на \In{Enter} вы подбрасываете кости (два 
    шестигранных кубика). После каждого раунда программа выводит 
    промежуточные результаты.
    

\item Напишите программу, которая принимает два аргумента:
    набор слов разделённых пробелами и файл. А выводит она
    строки файла, в которых встречается данное слово. 

    Воспользуйтесь стандартными функциями из модуля \In{Data.List}

\begin{code}
-- разбиение строки на подстроки по переносам каретки
lines :: String -> [String]

-- разбиение строки на подстроки по пробелам
words :: String -> [String]

-- возвращает True только в том случае, если 
-- первый список полностью содержится во втором
isInfixOf :: Eq a => [a] -> [a] -> Bool
\end{code}


\item Классы \In{Functor} и \In{Applicative} замкнуты относительно композиции. 
    Это свойство говорит о том, что композиция (аппликативных) функторов
    снова является (аппликативным) функтором. Докажите это!
    Пусть дан тип, который описывает композицию двух типов:

\begin{code}
newtype O f g a = O { unO :: f (g a)
\end{code}

    Определите экземпляры классов:

\begin{code}
instance (Functor f, Functor g) => Functor (O f g) where ...

instance (Applicative f, Applicative g) => Applicative (O f g) where ...
\end{code}

    Подсказка: если совсем не получается, ответ можно подсмотреть
    в библиотеке \In{TypeCompose}. Но пока мы не знаем как 
    устанавливать библиотеки и где они живут, всё-таки попытайтесь
    решить это упражнение самостоятельно.

\end{itemize}



