% source-highlight -s haskell -f latexcolor -i Kleisli.hs -o new.tex
\setcounter{chapter}{5}
\chapter{Специальные функции}

Мы научились комбинировать функции наиболее
общего типа \In{a -> b}. В этой главе мы посмотрим на специальные
функции и способы их комбинирования. Под специальными функциями
понимаются такие функции, результат которых имеет некоторую структуру.
Среди них функции, которые могут вычислить значение или 
упасть, или функции, которые возвращают сразу несколько вариантов значений.
Для составления таких функций из простейших в Haskell
предусмотрено несколько классов типов. Это функторы и монады. 
Их мы и рассмотрим в этой главе.

\section{Композиция функций}

Центральной функцией этой главы будет функция композиции.
Вспомним её определение для функций общего типа:

\begin{code}
(.) :: (b -> c) -> (a -> b) -> (a -> c)
f . g = \x -> f (g x)
\end{code}

Композиция двух функций \In{f} и \In{g} это такая функция,
в которой мы сначала применяем \In{g}, а затем \In{f}.
Для того чтобы тип функции стал более наглядным, мы
определим эту функцию немного по-другому. Мы поменяем 
аргументы местами. 

\begin{code}
(>>) :: (a -> b) -> (b -> c) -> (a -> c)
f >> g = \x -> g (f x)
\end{code}

На \RefFig{fun1} эта функция изображена графически. 
Мы будем изображать функции кружками, а значения -- стрелками.
Значения словно текут от узла к узлу по стрелкам. 
Поскольку тип стрелки выходящей из \In{f} совпадает
с типом стрелки входящей в \In{g} мы можем соединить их
и получить составную функцию \In{(f>>g)}.

\Fig{Композиция функций}{../pic/6/fun1.ps}{fun1}{0.8}


\subsection{Класс Category}

С помощью операции композиции можно обобщить понятие 
функции. Для этого существует класс \Classes{Category, категория}
\In{Category}:

\begin{code}
class Category cat where
    id   :: cat a a
    (>>) :: cat a b -> cat b c -> cat a c
\end{code}

Функция \In{cat} это тип с двумя параметрами, в котором выделено
специальное значение \In{id}, которое оставляет
аргумент без изменений. Также мы можем составлять
из простых функций сложные с помощью композиции,
если функции совпадают по типу.
Здесь мы для наглядности также заменили метод \In(.) на
\In{(>>)}, но суть остаётся прежней. 
Для любого экземпляра класса должны выполняться 
свойства:

\begin{code}
f  >> id  == f
id >> f   == f

f >> (g >> h) == (f >> g) >> h
\end{code}

Первые два свойства говорят о том, что \In{id} является
нейтральным элементом для \In{(>>)} слева и справа. 
Третье свойство говорит о том, что нам не важно в каком
порядке проводить композицию. Можно проверить, что эти
правила выполнены для функций.

\subsection{Специальные функции}

Все специальные функции, которые мы рассмотрим в этой главе
будут иметь один и тот же тип:

\begin{code}
a -> m b
\end{code}

Смотрите вместо произвольного типа \In{b} функция
возвращает \In{m b}. Единственное, что будет меняться
от раздела к разделу это тип \In{m}. Добавив этот тип
к результату, мы сузили область значений функции.
Простым примером таких функций могут быть функции, которые
возвращают списки:


\begin{code}
a -> [b]
\end{code}

Если раньше наши функции могли возвращать произвольное значение \In{b},
то теперь мы знаем, что все результирующие значения таких функций 
будут списками.

При этом для каждого такого \In{m} мы попытаемся
построить свой замкнутый мир специальных функций \In{a -> m b}. 
Он будет жить внутри  вселенной всех произвольных функций 
типа \In{a -> b}. В этом нам поможет специальный класс типов,
который называется категорией Клейсли (эта конструкция носит имя 
математика Хенрика Клейсли). 

\begin{code}
class Kleisli m where
    idK  :: a -> m a
    (*>) :: (a -> m b) -> (b -> m c) -> (a -> m c)    
\end{code}

Этот класс является классом \In{Category} в мире наших 
специальных функций. Если мы сотрём все буквы \In{m},
то мы получим обычные типы для тождества и композиции.
В этом мире должны выполняться те же правила:

\begin{code}
f   *> idK  == f
idK *> f    == f

f *> (g *> h) == (f *> g) *> h
\end{code}

\subsection{Взаимодействие с внешним миром}

С помощью класса \In{Kleisli} мы можем составлять из одних 
специальных функций другие. Но как мы сможем комбинировать
специальные функции с обычными?

Поскольку слева у нашей специальной функции обычный общий тип, 
то с этой стороны мы можем воспользоваться обычной функцией
композиции \In{>>}. Но как быть при композиции справа?
Нам нужна функция типа:

\begin{code}
(a -> m b) -> (b -> c) -> (a -> m c)
\end{code}

Оказывается мы можем составить её из методов класса \In{Kleisli}.
Мы назовём эту функцию композиции \In{(+>)}.

\begin{code}
(+>) :: Kleisli m => (a -> m b) -> (b -> c) -> (a -> m c)
f +> g = f *> (g >> idK)
\end{code}

С помощью метода \In{idK} мы можем погрузить в мир специальных
функций любую обычную функцию. 

\subsection{Три композиции}

У нас появилось много композиций целых три:

\begin{code}
аргументы                           |   результат 

обычная         >>  обычная         ==  обычная
специальная     +>  обычная         ==  специальная
специальная     *>  специальная     ==  специальная
\end{code}

При этом важно понимать, что по смыслу это три одинаковые
функции. Они обозначают операцию последовательного 
применения функций. Разные значки отражают разные типы 
функций аргументов.


\subsection{Обобщённая формулировка категории Клейсли}

Отметим, что мы могли бы сформулировать класс \In{Kleisli}
и в более общем виде с помощью класса \In{Category}:

\begin{code}
class Kleisli m where
    idK  :: Category cat => cat a (m a)
    (*>) :: Category cat => cat a (m b) -> cat b (m c) -> cat a (m c)

(+>) :: (Category cat, Kleisli m) 
    => cat a (m b) -> cat b c -> cat a (m c)
f +> g = f *> (g >> idK)
\end{code}

Мы заменили функциональный тип на его обобщение. Для наглядности
мы будем пользоваться специальной формулировкой со стрелочным типом. 

Для этого мы определим модуль \In{Kleisli.hs}


\begin{code}
module Kleisli where

import Prelude hiding (id, (>>))

class Category cat where
    id   :: cat a a
    (>>) :: cat a b -> cat b c -> cat a c

class Kleisli m where
    idK  :: a -> m a
    (*>) :: (a -> m b) -> (b -> m c) -> (a -> m c)    

(+>) :: Kleisli m => (a -> m b) -> (b -> c) -> (a -> m c)
f +> g = f *> (g >> idK)

-- Экземпляр для функций

instance Category (->) where
    id      = \x -> x
    f >> g  = \x -> g (f x)
\end{code}


Мы не будем импортировать функцию \In{id}, а определим её в классе 
\In{Category}. Также в \In{Prelude} уже определена функция 
\In{(>>)} мы спрячем её с помощью специальной директивы 
\KeyWord{hiding} \In{hiding}
для того, чтобы она нам не мешалась.
Далее мы будем дополнять этот модуль экземплярами класса \In{Kleisli}
и примерами.

\section{Примеры специальных функций}

\subsection{Частично определённые функции}

Частично определённые функции -- это такие функции, которые
определены не для всех значений аргументов. Примером такой
функции может быть функция поиска предыдущего числа для
натуральных чисел. Поскольку числа натуральные, то для
нуля такого числа нет. 
Для описания этого поведения мы можем воспользоваться 
специальным типом \Types{Maybe, частично определённые значения}
\In{Maybe}. Посмотрим на его определение:

\begin{code}
data Maybe a = Nothing | Just a
    deriving (Show, Eq, Ord)
\end{code}

\Fig{Частично определённая функция}{../pic/6/maybe0.ps}{maybe0}{0.4}

Частично определённая функция имеет тип \In{a -> Maybe b}, 
если всё в порядке и значение было вычислено, она вернёт 
\In{(Just a)}, а в случае ошибки будет возвращено значение
\In{Nothing}. На \RefFig{maybe0} изображена схема частично 
определённой функции. Теперь мы можем определить нашу функцию так:

\begin{code}
pred :: Nat -> Maybe Nat
pred Zero       = Nothing
pred (Succ a)   = Just a
\end{code}

Предыдущий элемент не определён для \In{Zero}. 

\subsubsection{Составляем функции вручную}

Значение функции \In{pred} завёрнуто в упаковку \In{Maybe}, и для того чтобы
воспользоваться им нам придётся разворачивать его каждый раз.
Как будет выглядеть функция извлечения дважды предыдущего 
натурального числа:

\begin{code}
pred2 :: Nat -> Maybe Nat
pred2 x = 
    case pred x of
        Just (Succ a) -> Just a
        _             -> Nothing
\end{code}

Если мы захотим определить \In{pred3}, мы заменим \In{pred} 
в \In{case}-выражении на \In{pred2}. Вроде не такое уж и длинное решение.
Но всё же мы теряем все преимущества гибких функций, все преимущества
бесточечного стиля. Нам бы хотелось написать так:

\begin{code}
pred2 :: Nat -> Maybe Nat
pred2 = pred >> pred

pred3 :: Nat -> Maybe Nat
pred3 = pred >> pred >> pred
\end{code}

Но компилятор этого не допустит.  

\subsubsection{Композиция}

Для того чтобы понять как устроена композиция частично определённых
функций изобразим её вычисление графически (\RefFig{maybe1}). 
Сверху изображены две частично определённых функции. 
Если функция \In{f} вернула значение, то оно подставляется 
в следующую частично определённую функцию. Если же первая функция
не смогла вычислить результат и вернула \In{Nothing}, то считается
что вся функция \In{(f*>g)} вернула \In{Nothing}. 

\Fig{Композиция частично определённых функций}%
{../pic/6/maybe1.ps}{maybe1}{0.8}


Теперь давайте закодируем это определение в Haskell. При этом 
мы воспользуемся нашим классом \In{Kleisli}. Аналогом
функции \In{id} для частично определённых функций будет функция,
которая просто заворачивает значение в конструктор \In{Just}.

\begin{code}
instance Kleisli Maybe where
    idK    = Just
    f *> g = \a -> case f a of
                        Nothing -> Nothing
                        Just b  -> g b
\end{code}

Смотрите, в \In{case}-выражении мы возвращаем \In{Nothing},
если функция \In{f} вернула \In{Nothing}, а если ей удалось
вычислить значение и она вернула \In{(Just b)} мы передаём
это значение в следующую функцию, т.е.~составляем выражение \mbox{\In{(g b)}}.

Сохраним это определение в модуле \In{Kleisli}, а также определение
для функции \In{pred} и загрузим модуль в интерпретатор.
Перед этим нам придётся добавить в список функций, 
которые мы не хотим импортировать из \In{Prelude}
функцию \In{pred}, она также уже определена в \In{Prelude}.
Для определения нашей функции нам потребуется модуль \In{Nat},
который мы уже определили. Скопируем файл \In{Nat.hs} в 
ту же директорию, в которой содержится файл \In{Kleisli.hs} 
и подключим этот модуль. Шапка модуля примет вид:

\begin{code}
module Kleisli where
    
import Prelude hiding(id, (>>), pred)
import Nat
\end{code}

Добавим определение экземпляра \In{Kleisli} для \In{Maybe} 
в модуль \In{Kleisli} а также определение функции \In{pred}.
Сохраним обновлённый модуль и загрузим в интерпретатор. 

\begin{code}
*Kleisli> :load Kleisli
[1 of 2] Compiling Nat              ( Nat.hs, interpreted )
[2 of 2] Compiling Kleisli          ( Kleisli.hs, interpreted )
Ok, modules loaded: Kleisli, Nat.
*Kleisli> let pred2 = pred *> pred
*Kleisli> let pred3 = pred *> pred *> pred
*Kleisli> let two   = Succ (Succ Zero)
*Kleisli> 
*Kleisli> pred two
Just (Succ Zero)
*Kleisli> pred3 two
Nothing
\end{code}

Обратите внимание на то, как легко определяются
производные функции. Желаемое поведение для частично
определённых функций закодировано в функции \In{(*>)}
теперь нам не нужно заворачивать значения и разворачивать
их из типа \In{Maybe}.

Приведём пример функции, которая составлена из частично
определённой функции и обычной. Определим функцию
\In{beside}, которая вычисляет соседей для данного числа Пеано.

\begin{code}
*Kleisli> let beside = pred +> \a -> (a, a + 2)
*Kleisli> beside Zero
Nothing
*Kleisli> beside two
Just (Succ Zero,Succ (Succ (Succ Zero)))
*Kleisli> (pred *> beside) two
Just (Zero,Succ (Succ Zero))
\end{code}

В выражении 

\begin{code}
pred +> \a -> (a, a + 2)
\end{code}

Мы сначала вычисляем предыдущее число, и если оно есть
составляем пару из \verb!\!\In{a -> (a, a+2)}, в пару попадёт 
данное число и число, следующее за ним через одно. Поскольку сначала
мы вычислили предыдущее число в итоговом кортеже окажется
предыдущее число и следующее. 

Итак с помощью функций из класса \In{Kleisli}
мы можем составлять частично определённые функции 
в бесточечном стиле. Обратите внимание на то, что
все функции кроме \In{pred} были составлены в интерпретаторе.

Отметим, что в \In{Prelude}
определена специальная функция \In{maybe}, которая
похожа на функцию \In{foldr} для списков, она заменяет
в значении типа \In{Maybe} конструкторы на функции. 
Посмотрим на её определение:

\begin{code}
maybe              :: b -> (a -> b) -> Maybe a -> b
maybe n f Nothing  =  n
maybe n f (Just x) =  f x
\end{code}

С помощью этой функции мы можем переписать определение
экземпляра \In{Kleisli} так:

\begin{code}
instance Kleisli Maybe where
    idM     = Just
    f *> g  = f >> maybe Nothing g
\end{code}

\subsection{Многозначные функции}

\Types{[a], списки}
Многозначные функции ветрены и непостоянны. Для некоторых
значений аргументов они возвращают одно значение, для иных
десять, а для третьих и вовсе ничего. В Haskell такие
функции имеют тип \In{a -> [b]}. Функция возвращает список 
ответов. На \RefFig{list0} изображена схема многозначной
функции.

\Fig{Многозначная функция}{../pic/6/list0.ps}{list0}{0.4}

Приведём пример. 
Системы Линденмайера (или L-системы) моделируют развитие живого организма.
Считается, что организм состоит из последовательности букв (или клеток).
В каждый момент времени одна буква заменяется на 
новую последовательность букв, согласно определённым правилам. 
Так организм живёт и развивается. Приведём пример:

\[\begin{array}{l}
    a \Ra ab \\
    b \Ra a \\
    \\
    a \\
    ab \\
    aba \\
    abaab \\
    abaababa \\
\end{array}\]

У нас есть два правила размножения клеток-букв в организме.
На каждом этапе мы во всём слове заменяем букву $a$ на слово $ab$ и букву
$b$ на $a$. Начав с одной буквы $a$, мы за несколько шагов
пришли к более сложному слову.

Опишем этот процесс в Haskell. Для этого определим правила развития
организма в виде многозначной функции:

\begin{code}
next :: Char -> String
next 'a' = "ab"
next 'b' = "a"
\end{code}

Напомню, что строки в Haskell являются списками символов.
Теперь нам нужно применить многозначную функцию к выходу
многозначной функции. Для этого мы воспользуемся классом
\In{Kleisli}.



\subsubsection{Композиция}

\Fig{Композиция многозначных функций}{../pic/6/list1.ps}{list1}{0.8}

Определим экземпляр класса \In{Kleisli} для списков. 
На \RefFig{list1} изображена схема композиции в случае
многозначных функций.
После применения первой функции \In{f}
мы применяем функцию к каждому элементу списка, который был получен
из \In{f}. Так у нас получится список списков. Но нам нужен
список, для этого мы после применения \In{g} объединяем
все значения в один большой список. Отметим, что функции \In{f}
и \In{g} в зависимости от значений могут возвращать разное 
число значений, поэтому на выходе у функций \In{g} разное
число стрелок.

Закодируем эту схему в Haskell:

\begin{code}
instance Kleisli [] where
    idK     = \a -> [a]
    f *> g  = f >> map g >> concat
\end{code}

Функция тождества принимает одно значение и погружает его в список.
В композиции мы сначала применяем \In{f} затем к каждому элементу
списка результата применяем \In{g}, так у нас получается список списков.
После чего мы сворачиваем его в один список с помощью функции \In{concat}.

Вспомним тип функций \In{map} и \In{concat}:

\begin{code}
map     :: (a -> b) -> [a] -> [b]
concat  :: [[a]] -> [a]
\end{code}

С помощью композиции мы можем получить n-тое поколение так:

\begin{code}
generate :: Int -> (a -> [a]) -> (a -> [a])
generate 0 f = f
generate n f = f *> generate (n - 1) f
\end{code}

Или мы можем воспользоваться функцией \In{iterate} и написать
это определение так:

\begin{code}
generate :: Int -> (a -> [a]) -> (a -> [a])
generate n f = iterate (*> f) f !! n
\end{code}

Функция \In{iterate} принимает функцию вычисления следующего
элемента и начальное значение и строит бесконечный список 
итераций:

\begin{code}
iterate :: (a -> a) -> a -> [a]
iterate f a = [a, f a, f (f a), f (f (f a)), ...]
\end{code}

Если мы подставим наши аргументы то мы получим список:

\begin{code}
[f, f*>f, f*>f*>f, f*>f*>f*>f, ...]
\end{code}

Проверим как работает эта функция в интерпретаторе. Для этого
мы сначала дополним наш модуль \In{Kleisli} определением экземпляра
для списков и функциями \In{next} и \In{generate}:

\begin{code}
*Kleisli> :reload
[2 of 2] Compiling Kleisli          ( Kleisli.hs, interpreted )
Ok, modules loaded: Kleisli, Nat.
*Kleisli> let gen n = generate n next 'a'
*Kleisli> gen 0
"ab"
*Kleisli> gen 1
"aba"
*Kleisli> gen 2
"abaab"
*Kleisli> gen 3
"abaababa"
*Kleisli> gen 4
"abaababaabaab"
\end{code}

Правила L-системы задаются многозначной функцией. Функция
\In{generate} позволяет по такой функции строить произвольное
поколение развития буквенного организма.



\section{Применение функций}

Давайте определим в терминах композиции ещё одну полезную
функцию. А именно функцию применения. Вспомним её тип:

\begin{code}
($) :: (a -> b) -> a -> b
\end{code}

Эту функцию можно определить через композицию, если
у нас есть в наличии постоянная функция и единичный тип.
Мы будем считать, что константа это функция из единичного
типа в значение. Превратив константу в функцию мы можем 
составить композицию:

\begin{code}
($) :: (a -> b) -> a -> b
f $ a = (const a >> f) ()
\end{code}

Зачем такое запутанное определение, вместо привычного \In{(f a)}?
Оказывается точно таким же способом мы можем определить 
применение в нашем мире специальных функций \In{a -> m b}.

Применение в этом мире происходит особенным образом.
Необходимо помнить о том, что второй аргумент функции применения,
значение, которое мы подставляем в функцию, также было получено 
из какой-то другой функции. Поэтому оно будет иметь такую же
форму, что и значения справа от стрелки. В нашем случае 
это \mbox{\In{m b}}.

Посмотрим на типы специальных функций применения:

\begin{code}
(*$) :: (a -> m b) -> m a -> m b
(+$) :: (a -> b)   -> m a -> m b
\end{code}

Функция \In{*$} применяет специальную функцию к специальному
значению, а функция \In{+$} применяет обычную функцию к 
специальному значению. Определения выглядят также как 
и в случае обычной функции применения, мы только меняем
знаки для композиции:

\begin{code}
f  $ a = (const a >> f) ()
f *$ a = (const a *> f) ()
f +$ a = (const a +> f) ()
\end{code}

Теперь мы можем не только нанизывать специальные функции
друг на друга но и применять их к значениям. Добавим
эти определения в модуль \In{Kleisli} и посмотрим 
как происходит применение в интерпретаторе. Одна тонкость
заключается в том, что мы определяли применение в терминах
класса \In{Kleisli}, поэтому правильно было написать типы 
новых функций так:

\begin{code}
infixr 0 +$, *$

(*$) :: Kleisli m => (a -> m b) -> m a -> m b
(+$) :: Kleisli m => (a -> b)   -> m a -> m b
\end{code}

Также мы определили приоритет выполнения операций.

Загрузим в интерпретатор:

\begin{code}
*Kleisli> let three = Succ (Succ (Succ Zero))
*Kleisli> pred *$ pred *$ idK three
Just (Succ Zero)
*Kleisli> pred *$ pred *$ idK Zero
Nothing
\end{code}

Обратите внимание на то как мы погружаем в мир
специальных функций обычное значение с помощью
функции \In{idK}. 

Вычислим третье поколение L-системы:

\begin{code}
*Kleisli> next *$ next *$ next *$ idK 'a'
"abaab"
\end{code}

Мы можем использовать и другие функции на списках:

\begin{code}
*Kleisli> next *$ tail $ next *$ reverse $ next *$ idK 'a'
"aba"
\end{code}

\subsection{Применение функций многих переменных}

С помощью функции \In{+$} мы можем применять к специальным
значениям обычные функции одного аргумента. А что если
нам захочется применить функцию двух аргументов?

Например если мы захотим сложить два частично определённых
числа:

\begin{code}
?? (+) (Just 2) (Just 2)
\end{code}

На месте \In{??} должна стоять функция типа:

\begin{code}
?? :: (a -> b -> c) -> m a -> m b -> m c
\end{code}

Оказывается с помощью методов класса \In{Kleisli} мы можем
определить такую функцию для любой обычной функции, а не только
для функции двух аргументов. Мы будем называть такие функции
словом \In{liftN}, где \In{N} -- число, указывающее на арность
функции. Функция \In{(liftN f)} \Quote{поднимает} (от англ.~lift)
обычную функцию \In{f} в мир специальных функций.

Функция \In{lift1} у нас уже есть, это просто функция \In{+$}.
Теперь давайте определим функцию \In{lift2}:

\begin{code}
lift2 :: Kleisli m => (a -> b -> c) -> m a -> m b -> m c
lift2 f a b = ...
\end{code}

Поскольку функция двух аргументов на самом деле является
функцией одного аргумента мы можем применить первый аргумент
с помощью функции \In{lift1}, посмотрим что у нас получится:

\begin{code}
lift1       :: (a' -> b') -> m' a' -> m' b'
f           :: (a -> b -> c)
a           :: m a

lift1 f a   :: m (b -> c)  -- m' == m, a' == a, b' == b -> c
\end{code}

Теперь в нашем определении для \In{lift2} появится
новое слагаемое \In{g}:

\begin{code}
lift2 :: Kleisli m => (a -> b -> c) -> m a -> m b -> m c
lift2 f a b = ...
    where g = lift1 f a
\end{code}

Один аргумент мы применили, осталось применить второй.
Нам нужно составить выражение \In{(g b)}, но 
для этого нам нужна функция типа:

\begin{code}
m (b -> c) -> m b -> m c
\end{code}

Эта функция применяет к специальному значению 
функцию, которая завёрнута в тип \In{m}. 
Посмотрим на определение этой функции, мы назовём её
\In{$$}:

\begin{code}
($$) :: Kleisli m => m (a -> b) -> m a -> m b
mf $$ ma = ( +$ ma) *$ mf
\end{code}

Вы можете убедиться в том, что это определение проходит проверку типов.
Посмотрим как эта функция работает в интерпретаторе на примере
частично определённых и многозначных функций, для этого давайте
добавим в модуль \In{Kleisli} это определение и загрузим
его в интерпретатор:

\begin{code}
*Kleisli> :reload Kleisli
Ok, modules loaded: Kleisli, Nat.
*Kleisli> Just (+2) $$ Just 2
Just 4
*Kleisli> Nothing $$ Just 2
Nothing
*Kleisli> [(+1), (+2), (+3)] $$ [10,20,30]
[11,21,31,12,22,32,13,23,33]
*Kleisli> [(+1), (+2), (+3)] $$ []
[]
\end{code}

Обратите внимание на то, что в случае списков были составлены
все возможные комбинации применений. Мы применили первую функцию
из списка ко всем аргументам, потом вторую функцию, третью и объединили
все результаты в список.

Теперь мы можем закончить наше определение для \In{lift2}:

\begin{code}
lift2 :: Kleisli m => (a -> b -> c) -> m a -> m b -> m c
lift2 f a b = f' $$ b
    where f' = lift1 f a
\end{code}

Мы можем записать это определение более кратко:

\begin{code}
lift2 :: Kleisli m => (a -> b -> c) -> m a -> m b -> m c
lift2 f a b = lift1 f a $$ b
\end{code}

Теперь давайте добавим это определение в модуль \In{Kleisli}
и посмотрим в интерпретаторе как работает эта функция:

\begin{code}
*Kleisli> :reload
[2 of 2] Compiling Kleisli          ( Kleisli.hs, interpreted )
Ok, modules loaded: Kleisli, Nat.
*Kleisli> lift2 (+) (Just 2) (Just 2)
Just 4
*Kleisli> lift2 (+) (Just 2) Nothing
Nothing
\end{code}

Как на счёт функций трёх и более аргументов? У нас уже
есть функции \In{lift1} и \In{lift2} определим функцию
\In{lift3}:

\begin{code}
lift3 :: Kleisli m => (a -> b -> c -> d) -> m a -> m b -> m c -> m d
lift3 f a b c = ...
\end{code}

Первые два аргумента мы можем применить с помощью 
функции \In{lift2}. Посмотрим на тип получившегося выражения:

\begin{code}
lift2       :: Kleisli m => (a' -> b' -> c') -> m a' -> m b' -> m c'
f           :: a -> b -> c -> d

lift2 f a b :: m (c -> d)   -- a' == a, b' == b, c' == c -> d
\end{code}

У нас опять появился тип \In{m (c -> d)} и к нему нам нужно
применить значение \In{m c}, чтобы получить \In{m d}. Этим 
как раз и занимается функция \In{$$}. Итак итоговое определение
примет вид:

\begin{code}
lift3 :: Kleisli m => (a -> b -> c -> d) -> m a -> m b -> m c -> m d
lift3 f a b c = lift2 f a b $$ c
\end{code}

Так мы можем определить любую функцию \In{liftN} через
функции \In{liftN-1} и \In{$$}.

\subsection{Несколько полезных функций}

Теперь мы умеем применять к специальным значениям
произвольные обычные функции. Определим ещё несколько 
полезных функций. Первая функция принимает список
специальных значений и собирает их в специальный список:

\begin{code}
import Prelude hiding (id, (>>), pred, sequence)

sequence :: Kleisli m => [m a] -> m [a]
sequence = foldr (lift2 (:)) (idK []) 
\end{code}

Мы \Quote{спрячем} из \In{Prelude} одноимённую
функцию \In{sequence}. Посмотрим на примеры:

\begin{code}
*Kleisli> sequence [Just 1, Just 2, Just 3]
Just [1,2,3]
*Kleisli> sequence [Just 1, Nothing, Just 3]
Nothing
\end{code}

Во второй команде вся функция вернула \In{Nothing} потому что
при объединении списка встретилось значение \In{Nothing}, это
равносильно тому, что мы объединяем в один список, значения
полученные из функций, которые могут не вычислить результат. 
Поскольку значение одного из элементов не определено, весь список
не определён.

Посмотрим как работает эта функция на списках:

\begin{code}
*Kleisli> sequence [[1,2,3], [11,22]]
[[1,11],[1,22],[2,11],[2,22],[3,11],[3,22]]
\end{code}

Она составляет список всех комбинаций элементов из всех
подсписков. 

С помощью этой функции мы можем определить функцию \In{mapK}.
Эта функция является аналогом обычной функции \In{map}, но
она применяет специальную функцию к списку значений.

\begin{code}
mapK :: Kleisli m => (a -> m b) -> [a] -> m [b]
mapK f = sequence . map f
\end{code}

\section{Функторы и монады}

В этой главе мы выписали вручную все определения для
класса \In{Kleisli}. Мы сделали это потому, что на самом
деле в арсенале стандартных средств Haskell такого 
класса нет. Класс \In{Kleisli} строит замкнутый 
мир специальных функций \mbox{\In{a -> m b}}. Его цель построить язык 
в языке и сделать программирование со специальными функциями
таким же удобным как и с обычными функциями. 
Мы пользовались классом \In{Kleisli} исключительно в целях 
облегчения понимания этого мира. Впрочем никто не мешает
нам определить этот класс и пользоваться им в наших программах.

А пока посмотрим, что есть в Haskell и как это соотносится 
с тем, что мы уже увидели. С помощью класса \In{Kleisli}  
мы научились делать три различных операции применения:

Применение:
\begin{itemize}
\item обычных функций одного аргумента к
        специальным значениям (функция \In{+$}). 

\item обычных функций произвольного числа аргументов к
        специальным значениям (функции \In{+$} и \In{$$})
        
\item специальных функций к специальным значениям
        (функция \In{*$}).

\end{itemize}

В Haskell для решения этих задач предназначены три отдельных
класса. Это функторы, аппликативные функторы и монады. 

\subsection{Функторы}

Посмотрим на определение класса \Classes{Functor, функторы} \In{Functor}:

\begin{code}
class Functor f where
    fmap :: (a -> b) -> f a -> f b
\end{code}

Тип метода \In{fmap} совпадает с типом для функции \In{+$}:

\begin{code}
(+$) :: Kleisli m => (a -> b) -> m a -> m b
\end{code}

Нам только нужно заменить \In{m} на \In{f} и 
зависимость от \In{Kleisli} на зависимость от \In{Functor}:

Итак в Haskell у нас есть базовая операция \In{fmap}
применения обычной функции к значению из мира 
специальных функций. В модуле \In{Control.Applicative}
определён инфиксный синоним \In{<$>} для этой функции.

\subsection{Аппликативные функторы}

Посмотрим на определение класса \Classes{Applicative, аппликативные функторы}
\In{Applicative}:

\begin{code}
class Functor f => Applicative f where
        pure    :: a -> f a
        (<*>)   :: f (a -> b) -> f a -> f b
 \end{code}

Если присмотреться к типам методов этого класса, то
мы заметим, что это наши старые знакомые \In{idK} и \In{$$}.
Если для данного типа \In{f} определён экземпляр класса
\In{Applicative}, то из контекста следует, что для него
также определён и экземпляр класса \In{Functor}. 

Значит у нас есть функции \In{fmap} (или \In{lift1}) 
и \In{<*>} (или \In{$$}). С их помощью мы можем 
составить функции \In{liftN}, которые поднимают обычные 
функции произвольного числа аргументов в мир специальных
значений. 

Класс \In{Applicative} определён в модуле \In{Control.Applicative},
там же мы сможем найти и функции \In{liftA}, \In{liftA2}, \In{liftA3}
и символьный синоним \In{<$>} для функции \In{fmap}.
Функции \In{liftAn} определены так:

\begin{code}
liftA2 f a b   = f <$> a <*> b
liftA3 f a b c = f <$> a <*> b <*> c
\end{code}

Видно что эти определения с точностью до обозначений 
совпадают с теми, что мы уже писали для класса \In{Kleisli}.

\subsection{Монады}

Посмотрим на определение класса \Classes{Monad, монады} \In{Monad} 

\begin{code}
class Monad m where
  return :: a -> m a
  (>>=)  :: m a -> (a -> m b) -> m b
\end{code}

Присмотримся к типам методов этого класса:

\begin{code}
return :: a -> m a
\end{code}

Их типа видно, что это ни что иное как функция \In{idK}. 
В классе \In{Monad} у неё точно такой же смысл. 
Теперь функция \In{>>=}, она читается как функция 
\emph{связывания} (bind). 

\begin{code}
(>>=)  :: m a -> (a -> m b) -> m b
\end{code}

Так возможно совпадение не заметно, но давайте \Quote{перевернём}
эту функцию:

\begin{code}
(=<<)  :: Monad m => (a -> m b) -> m a -> m b
(=<<) = flip (>>=)
\end{code}

Поменяв аргументы местами, мы получили знакомую функцию \In{*$}.
Итак функция связывания это функция применения специальной
функции к специальному значению. У неё как раз такой смысл.

В \In{Prelude} определены экземпляры класса \In{Monad}
для типов \In{Maybe} и \In{[]}.  
Они определены по такому же принципу,
что и наши определения для \In{Kleisli} только не 
для композиции, а для применения. 

Отметим, что в модуле \In{Control.Monad} определены 
функции \In{sequence} и \In{mapM}, они несут тот
же смысл, что и функции \In{sequence} и \In{mapК},
которые мы определяли для класса \In{Kleisli}. 

\subsection{Свойства классов}

Посмотрим на свойства функторов и аппликативных функторов.

\subsubsection{Свойства класса Functor}

\begin{code}
fmap id x           == x                -- тождество
fmap f . fmap g     == fmap (f . g)     -- композиция
\end{code}

Первое свойство говорит о том, что если мы применяем
\In{fmap} к функции тождества, то мы должны снова получить
функцию тождества, или по другому можно сказать, что 
применение функции тождества к специальному значению не 
изменяет это значение. Второе свойство говорит о том, что
последовательное применение к специальному значению двух 
обычных функций можно записать в виде применения композиции
двух обычных функций к специальному значению.

Если всё это звучит туманно, попробуем переписать эти свойства
в терминах композиции:

\begin{code}
mf +> id         == mf
(mf +> g) +> h   == mf +> (g >> h)
\end{code}

Первое свойство говорит о том, что тождественная функция
не изменяет значение при композиции. Второе свойство указывает на 
ассоциативность композиции одной специальной функции \In{mf} 
и двух обычных функций \mbox{\In{g} и \In{h}}.


\subsubsection{Свойства класса Applicative}

Свойства класса \In{Applicative}, для наглядности они сформулированы
не через методы класса, а через производные функции.

\begin{code}
fmap f x            == liftA f x             -- связь с Functor

liftA  id x         == x                     -- тождество
liftA3 (.) f g x    == f <*> (g <*> x)       -- композиция 
liftA  f (pure x)   == pure (f x)            -- гомоморфизм
\end{code}

Первое свойство говорит о том, что применение специальной функции
одного аргумента совпадает с методом \In{fmap} из класса \In{Functor}.
Свойство тождества идентично аналогичному свойству для класса
\In{Functor}. 

Свойство композиции сформулировано хитро, но давайте 
посмотрим на типы аргументов:

\begin{code}
(.) :: (b -> c) -> (a -> b) -> (a -> c)
f   :: m (b -> c)
g   :: m (a -> b)    
x   :: m a

liftA3 (.) f g x :: m c

g <*> x      :: m b
f (g <*> x)  :: m c
\end{code}

Слева в свойстве стоит \In{liftA3}, а не \In{liftA2},
потому что мы сначала применяем композицию \In{(.)} к двум функциям
\In{f} и \In{g}, а затем применяем составную функцию к значению \In{x}.

Последнее свойство говорит о том, что если мы возьмём обычную функцию
и обычное значение и поднимем их в мир специальных значений с
помощью \In{lift} и \In{pure}, то это тоже самое если бы
мы просто применили бы функцию \In{f} к значению в мире обычных
значений и затем подняли бы результат в мир специальных значений.


\subsection{Полное определение классов}

На самом деле я немного схитрил. Я рассказал вам только
об основных методах классов \In{Applicative} и \In{Monad}.
Но они содержат ещё несколько дополнительных методов,
которые выражаются через остальные. Посмотрим на них, 
начнём с класса \In{Applicative}.

\begin{code}
class Functor f => Applicative f where
        -- | Поднимаем значение в мир специальных значений.
        pure :: a -> f a

        -- | Применение специального значения-функции.
        (<*>) :: f (a -> b) -> f a -> f b

        -- | Константная функция. Отбрасываем первое значение.
        (*>) :: f a -> f b -> f b
        (*>) = liftA2 (const id)
        
        -- | Константная функция, Отбрасываем второе значение.
        (<*) :: f a -> f b -> f a
        (<*) = liftA2 const
\end{code}

Два новых метода \In{(*>)} и \In{(<*)} имеют смысл
константных функций. Первая функция игнорирует значение слева,
а вторая функция игнорирует значение справа. 
Посмотрим как они работают в интерпретаторе:

\begin{code}
Prelude Control.Applicative> Just 2 *> Just 3
Just 3
Prelude Control.Applicative> Nothing *> Just 3
Nothing
Prelude Control.Applicative> (const id) Nothing  Just 3
Just 3
Prelude Control.Applicative> [1,2] <* [1,2,3]
[1,1,1,2,2,2]
\end{code}

Значение игнорируется, но способ комбинирования специальных
функций учитывается. Так во втором выражении не смотря на 
то, что мы не учитываем конкретное значение \In{Nothing},
мы учитываем, что если один из аргументов частично определённой
функции не определён, то не определено всё значение. 
Сравните с результатом выполнения следующего выражения. 

По той же причине в последнем выражении мы получили три
копии первого списка. Так произошло потому, что второй
список содержал три элемента. К каждому из элементов
была применена функция \In{const x}, где \In{x} пробегает
по элементам списка слева от \In{(<*)}.

Аналогичный метод есть и в классе \In{Monad}:

\begin{code}
class  Monad m  where
    return  :: a -> m a
    (>>=)   :: m a -> (a -> m b) -> m b
    
    (>>)    :: m a -> m b -> m b
    fail    :: String -> m a

    m >> k  = m >>= const k
    fail s  = error s
\end{code}

Функция \In{>>} в классе \In{Monad}, которую
мы прятали из-за символа композиции, является 
аналогом постоянной функции в классе \In{Monad}.
Она работает так же как и \In{*>}. 
Функция \In{fail} используется для служебных нужд Haskell при выводе
ошибок. Поэтому мы её здесь не рассматриваем. Для определения 
экземпляра класса \In{Monad} достаточно определить методы
\In{return} и \In{>>=}.

\subsection{Исторические замечания}

Напрашивается вопрос.
Зачем нам функции \In{return} и \In{pure} или \In{*>} и \In{>>}?
Если вы заглянете в документацию к модулю \In{Control.Monad},
то там вы найдёте функции \In{liftM}, \In{liftM2}, \In{liftM3},
которые выполняют те же операции, что и аналогичные функции
из модуля \In{Control.Applicative}.

Стандартные библиотеки устроены так, потому что класс 
\In{Applicative} появился гораздо позже класса \In{Monad}. 
И к появлению этого нового класса уже накопилось огромное 
число библиотек, которые рассчитаны на прежние имена. 
Но в будущем возможно прежние классы будут заменены на такие классы:

\begin{code}
class Functor f where
    fmap :: (a -> b) -> f a -> f b

class Pointed f where
    pure :: a -> f a

class (Functor f, Pointed f) => Applicative f where
    (<*>) :: f (a -> b) -> f a -> f b

    (*>)  :: f a -> f b -> f b
    (<*)  :: f a -> f b -> f a

class Applicative f => Monad f where
    (>>=) :: f a -> (a -> f b) -> f b
\end{code}


\section{Краткое содержание}

В этой главе мы долгой обходной дорогой шли к понятию монады
и функтора. Эти классы служат для облегчения работы в мире
специальных функций вида \In{a -> m b}, в категории Клейсли

С помощью класса \In{Functor} можно применять специальные
значения к обычным функциям одного аргумента:

\begin{code}
class Functor f where
    fmap :: (a -> b) -> f a -> f b
\end{code}

С помощью класса \In{Applicative} можно применять специальные
значения к обычным функциям любого числа аргументов:

\begin{code}
class Functor f => Applicative f where
    pure    :: a -> f a
    <*>     :: f (a -> b) -> f a -> f b

liftA  :: Applicative f => (a -> b) -> f a -> f b
liftA2 :: Applicative f => (a -> b -> c) -> f a -> f b -> f c
liftA3 :: Applicative f => (a -> b -> c -> d) -> f a -> f b -> f c -> f d
...
\end{code}


С помощью класса \In{Monad} можно применять специальные
значения к специальным функциям.

\begin{code}
class Monad m where
    return  :: a -> m a
    (>>=)   :: m a -> (a -> m b) -> m b
\end{code}

Функция \In{return} является функцией \In{id} в 
мире специальных функций, а функция \In{>>=} 
является функцией применения \In{($)}, с обратным 
порядком следования аргументов. Вспомним также
класс \In{Kleisli}, на примере котором мы узнали много
нового из жизни специальных функций:

\begin{code}
class Kleisli m where
    idK     :: a -> m a
    (*>)    :: (a -> m b) -> (b -> m c) -> (a -> m c)
\end{code}

Мы узнали несколько стандартных специальных функций:

\subsubsection{Частично определённые функции}

\begin{code}
a -> Maybe b
data Maybe a = Nothing | Just a
\end{code}


\subsubsection{Многозначные функции}

\begin{code}
a -> [b]
data [a] = [] | a : [a]
\end{code}

\section{Упражнения}

 В первых упражнениях вам предлагается по
 картинке специальной функции написать экземпляр
 классов \In{Kleisli} и \In{Monad}. 


\subsubsection{Функции с состоянием}

\Fig{Функция с состоянием}{../pic/6/state0.ps}{state0}{0.4}

\Types{State, cостояние}
В Haskell нельзя изменять значения. Новые сложные значения
описываются в терминах базовых значений. Но как же тогда
мы сможем описать функцию с состоянием? Функцию,
которая принимает на вход значение, составляет результат
на основе внутреннего состояния и значения аргумента и
обновляет состояние. Поскольку мы не можем изменять состояние
единственное, что нам остаётся -- это принимать значение
состояния на вход вместе с аргументом и возвращать
обновлённое состояние на выходе. У нас получится такой тип:

\begin{code}
a -> s -> (b, s)
\end{code}

Функция принимает одно значение типа \In{a} и 
состояние типа \In{s}, а возвращает пару, которая
состоит из результата типа \In{b} и обновлённого 
состояния. Если мы введём синоним:

\begin{code}
type State s b = s -> (b, s)
\end{code}

И вспомним о частичном применении, то мы сможем
записать тип функции с состоянием так:

\begin{code}
a -> State s b 
\end{code}

В Haskell пошли дальше и выделили для таких
функций специальный тип:


\begin{code}
data State s a = State (s -> (a, s))

runState :: State s a -> s -> (a, s)
runState (State f) = f
\end{code}

\Fig{Композиция функций с состоянием}{../pic/6/state1.ps}{state1}{0.8}

Функция \In{runState} просто извлекает функцию
из оболочки \In{State}.

На \RefFig{state0} изображена схема функции с состоянием.
В сравнении с обычной функцией у такой функции один дополнительный 
выход и один дополнительный вход типа \In{s}. По ним течёт и
изменяется состояние. 

Попробуйте по схеме композиции для функций с состоянием
написать экземпляры для классов \In{Kleisli} и \In{Monad}
для типа \In{State s} (\RefFig{state1}). 

Подсказка: В этом определении есть одна хитрость,
в отличае от типов \In{Maybe} и \In{[a]} у типа
\In{State} два параметра, это параметр состояния 
и параметр значения. Но мы делаем экземпляр не
для \In{State}, а для \In{State s}, то есть мы
свяжем тип с некоторым произвольным типом \In{s}.

\begin{code}
instance Kleisli (State s) where
	...
\end{code}


\subsubsection{Функции с окружением}

\Types{Reader, значения с окружением}
Сначала мы рассмотрим функции с окружением. 
Функции с окружением -- это такие функции, у которых
есть некоторое хранилище данных или окружение, из которых
они могут читать информацию. Но в отличие от функций
с состоянием они не могут это окружение изменять. 
Функция с окружением похожа на функцию с состоянием без одного
выхода для состояния (\RefFig{reader0}).

\Fig{Функция с окружением}{../pic/6/reader0.ps}{reader0}{0.4}

Функция с окружением принимает аргумент \In{a} и окружение \In{env}
и возвращает результат \In{b}:

\begin{code}
a -> env -> b
\end{code}

Как и в случае функций с состоянием выделим для функции
с окружением отдельный тип. В Haskell он называется \In{Reader}
(от англ.~чтец). Все функции с окружением имеют возможность
читать из общего хранилища данных. Например они могут иметь
доступ на чтение к общей базе данных. 

\begin{code}
data Reader env b = Reader (env -> b)

runReader :: Reader env b -> (env -> b)
runReader (Reader f) = f
\end{code}

Теперь функция с окружением примет вид:

\begin{code}
a -> Reader env b
\end{code}

Определите для функций с окружением экземпляр класса \In{Kleisli}.
У нас возникнет цепочка функций, каждая из которых будет
нуждаться в значении окружения. Поскольку окружение общее для 
всех функций мы всем функциям передадим одно и то же значение
(\RefFig{reader1}).

\Fig{Функция с окружением}{../pic/6/reader1.ps}{reader1}{0.8}

\subsubsection{Функции-накопители}

\Types{Writer, знчения-накопители}
Функции-накопители при вычислении за ширмой накапливают
некоторое значение. Функция-накопитель похожа на 
функцию с состоянием но без стрелки, по которой состояние подаётся
в функцию (\RefFig{writer0}).
Функция-накопитель имеет тип: \In{a -> (b, msg)}

\Fig{Функция-накопитель}{../pic/6/writer0.ps}{writer0}{0.4}

Выделим результат функции в отдельный тип с именем \In{Writer}. 

\begin{code}
data Writer msg b = Writer (b, msg)

runWriter :: Writer msg b -> (b, msg)
runWriter (Writer a) = a
\end{code}

Тип функции примет вид:

\begin{code}
a -> Writer msg b
\end{code}

Значения типа \In{msg} мы будем называть сообщениями.
Смысл функций \mbox{\In{a -> Writer msg b}} заключается в том, что
при вычислении они накапливают в значении \In{msg}
какую-нибудь информацию. Это могут быть отладочные сообщения.
Или база данных, которая открыта для всех функций на запись. 

\subsubsection{Класс Monoid}

Как мы будем накапливать результат? Пока мы умеем лишь 
возвращать из функции пару значений. Одно из них нам 
нужно передать в следующую функцию, а что делать с другим? 

На помощь нам придёт класс \Classes{Monoid, моноиды}
\In{Monoid}, он определён в
модуле \In{Data.Monoid}:

\begin{code}
class Monoid a where
    mempty  :: a
    mappend :: a -> a -> a
\end{code}

В этом классе определено пустое значение \In{mempty}
и бинарная функция соединения двух значений в одно.
Этот класс очень похож на класс \In{Category} и 
\In{Kleisli}. Там тоже было значение, которое ничего не делает
и операция составления нового значения из двух простейших значений.
Даже свойства класса похожи:

\begin{code}
mempty  `mappend` f         = f
f       `mappend` mempty    = f

f `mappend` (g `mappend` h) =  (f `mappend` g) `mappend` h 
\end{code}

\Fig{Композиция функций-накопителей}{../pic/6/writer1.ps}{writer1}{0.8}

Первые два свойства говорят о том, что значение
\In{mempty} и вправду является пустым элементом относительно
операции \In{mappend}. А третье свойство говорит о том, что
порядок при объединении элементов не важен.

Посмотрим на определение экземпляра для списков:

\begin{code}
instance Monoid [a] where
    mempty  = []
    mappend = (++)
\end{code}

Итак пустой элемент это пустой список, а объединение 
это операция конкатенации списков. Проверим в интерпретаторе:

\begin{code}
*Kleisli> :m Data.Monoid
Prelude Data.Monoid> [1 .. 4] `mappend` [4, 3 .. 1]
[1,2,3,4,4,3,2,1]
Prelude Data.Monoid> "Hello" `mappend` " World" `mappend` mempty
"Hello World"
\end{code}

Напишите экземпляр класса \In{Kleisli} для функций 
накопителей по \RefFig{writer1}. При этом будем считать, что тип \In{msg} 
является экземпляром класса \In{Monoid}.

\subsubsection{Экземпляры для функторов и монад}

Представьте, что у нас нет класса \In{Kleisli}, а есть
лишь \In{Functor}, \In{Applicative} и \In{Monad}. Напишите экземпляры
для этих классов для всех рассмотренных в этой главе
специальных функций (в том числе и для \In{Reader} и \In{Writer}).
Экземпляры \In{Functor} и \In{Applicative} могут быть определены
через \In{Monad}. Но для тренировки определите экземпляры полностью. 
Сначала \In{Functor}, затем \In{Applicative} и в последнюю очередь
\In{Monad}.

\subsubsection{Деревья}

Напишите экземпляры классов \In{Kleisli} и \In{Monad}
для двух типов, которые описывают деревья. Бинарные деревья:

\begin{code}
data BTree a = BList a | BNode a (BTree a) (BTree a)
\end{code}

Деревья с несколькими узлами:

\begin{code}
data Tree a = Node a [Tree a]
\end{code}

Считайте, что списки являются частными случаями деревьев.
В этом смысле деревья будут описывать многозначные функции,
которые возвращают несколько значений, организованных 
в иерархическую структуру. 

\subsubsection{Стандартные функции}

Почитайте документацию к модулям \In{Control.Monad}
и \In{Control.Applicative}. Присмотритесь к функциям, 
попробуйте применить их в интерпретаторе. 

\subsubsection{Эквивалентность классов Kleisli и Monad}

Покажите, что классы \In{Kleisli} и \In{Monad}
эквивалентны. Для этого нужно для произвольного
типа c с одним параметром \In{m} определить два экземпляра: 

\begin{code}
instance Kleisli m => Monad   m where
instance Monad   m => Kelisli m where
\end{code}

Нужно определить экземпляр одного класса с помощью методов другого.

\subsubsection{Свойства класса Monad}

Если класс \In{Monad} эквивалентен \In{Kleisli},
то в нём должны выполнятся точно такие же свойства.
Запишите свойства класса \In{Kleisli} через методы
класса \In{Monad} 
